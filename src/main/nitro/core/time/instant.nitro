
// Represents a point in time
struct Instant {
    seconds: Long
    nanoseconds: Int
}

// Return the current instant
@Extern [lib: "core", name: "instant_now"]
fun Instant::now(): Instant {}

@Extern [lib: "core", name: "instant_format_to_iso8601"]
fun Instant.format_to_iso8601(): String {}

// Return the instant from the unix timestamp in seconds
fun Instant::from_unix_timestamp(seconds: Long): Instant {
    ret Instant @[
        seconds: seconds
        nanoseconds: 0
    ]
}

// Return the unix timestamp in seconds
fun Instant.get_unix_timestamp(): Long {
    ret this.seconds
}

fun Instant.elapsed(): Duration {
    ret Instant::now().elapsed_since(this)
}

fun Instant.elapsed_since(other: Instant): Duration {
    let seconds = this.seconds - other.seconds
    let nanoseconds = this.nanoseconds - other.nanoseconds

    if (nanoseconds < 0) {
        seconds -= 1L
        nanoseconds += 1_000_000_000
    }

    ret Duration @[
        seconds: seconds
        nanoseconds: nanoseconds
    ]
}

fun Instant.add_duration(duration: Duration): Instant {
    let seconds = this.seconds + duration.seconds
    let nanoseconds = this.nanoseconds + duration.nanoseconds

    if (nanoseconds >= 1_000_000_000) {
        seconds += 1L
        nanoseconds -= 1_000_000_000
    }

    ret Instant @[
        seconds: seconds
        nanoseconds: nanoseconds
    ]
}

fun Instant.get_ordering(other: Instant): Ordering {
    ret when {
        this.seconds < other.seconds -> Ordering::Less
        this.seconds > other.seconds -> Ordering::Greater
        this.nanoseconds < other.nanoseconds -> Ordering::Less
        this.nanoseconds > other.nanoseconds -> Ordering::Greater
        else -> Ordering::Equals
    }
}

fun Instant.to_string(): String {
    ret "Instant @[seconds: ${this.seconds}, nanoseconds: ${this.nanoseconds}]"
}
