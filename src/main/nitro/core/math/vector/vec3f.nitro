mod math {

    // 3D Vector with Float components
    // Represents a direction or a position in 3D space.
    struct Vec3f {
        x: Float
        y: Float
        z: Float
    }

    fun Vec3f::new(): Vec3f {
        ret Vec3f @[x: 0.0, y: 0.0, z: 0.0]
    }

    fun Vec3f::new(x: Float, y: Float, z: Float): Vec3f {
        ret Vec3f @[x: x, y: y, z: z]
    }

    fun Vec3f.set_zero() {
        x = 0.0
        y = 0.0
        z = 0.0
    }

    fun Vec3f.set(x: Float, y: Float, z: Float) {
        this.x = x
        this.y = y
        this.z = z
    }

    fun Vec3f.set(other: Vec3f) {
        x = other.x
        y = other.y
        z = other.z
    }

    fun Vec3f.get(index: Int): Float {
        when index {
            0 -> ret x
            1 -> ret y
            2 -> ret z
            else -> crash("Index out of bounds for Vec3f: $index")
        }
    }

    fun Vec3f.set(index: Int, value: Float) {
        when index {
            0 -> { x = value }
            1 -> { y = value }
            2 -> { z = value }
            else -> crash("Index out of bounds for Vec3f: $index")
        }
    }

    // Add

    fun Vec3f.add(scalar: Float): Vec3f {
        ret Vec3f @[x: x + scalar, y: y + scalar, z: z + scalar]
    }

    fun Vec3f.add(other: Vec3f): Vec3f {
        ret Vec3f @[x: x + other.x, y: y + other.y, z: z + other.z]
    }

    fun Vec3f.add_assign(other: Float) {
        x += other
        y += other
        z += other
    }

    fun Vec3f.add_assign(other: Vec3f) {
        x += other.x
        y += other.y
        z += other.z
    }

    // Subtract

    fun Vec3f.sub(scalar: Float): Vec3f {
        ret Vec3f @[x: x - scalar, y: y - scalar, z: z - scalar]
    }

    fun Vec3f.sub(other: Vec3f): Vec3f {
        ret Vec3f @[x: x - other.x, y: y - other.y, z: z - other.z]
    }

    fun Vec3f.sub_assign(other: Float) {
        x -= other
        y -= other
        z -= other
    }

    fun Vec3f.sub_assign(other: Vec3f) {
        x -= other.x
        y -= other.y
        z -= other.z
    }

    // Multiply

    fun Vec3f.mul(scalar: Float): Vec3f {
        ret Vec3f @[x: x * scalar, y: y * scalar, z: z * scalar]
    }

    fun Vec3f.mul(other: Vec3f): Vec3f {
        ret Vec3f @[x: x * other.x, y: y * other.y, z: z * other.z]
    }

    fun Vec3f.mul_assign(scalar: Float) {
        x *= scalar
        y *= scalar
        z *= scalar
    }

    fun Vec3f.mul_assign(other: Vec3f) {
        x *= other.x
        y *= other.y
        z *= other.z
    }

    // Divide

    fun Vec3f.div(scalar: Float): Vec3f {
        ret Vec3f @[x: x / scalar, y: y / scalar, z: z / scalar]
    }

    fun Vec3f.div(other: Vec3f): Vec3f {
        ret Vec3f @[x: x / other.x, y: y / other.y, z: z / other.z]
    }

    fun Vec3f.div_assign(scalar: Float) {
        x /= scalar
        y /= scalar
        z /= scalar
    }

    fun Vec3f.div_assign(other: Vec3f) {
        x /= other.x
        y /= other.y
        z /= other.z
    }

    // Special Operations

    // Dot product of two vectors
    fun Vec3f.dot(other: Vec3f): Float {
        ret x * other.x + y * other.y + z * other.z
    }

    // Cross product of two vectors
    fun Vec3f.cross(other: Vec3f): Vec3f {
        ret Vec3f @[
            x: y * other.z - z * other.y,
            y: z * other.x - x * other.z,
            z: x * other.y - y * other.x,
        ]
    }

    fun Vec3f.set_to_cross(one: Vec3f, other: Vec3f) {
        x = one.y * other.z - one.z * other.y
        y = one.z * other.x - one.x * other.z
        z = one.x * other.y - one.y * other.x
    }

    fun Vec3f.get_len(): Float {
        ret sqrt(x * x + y * y + z * z)
    }

    fun Vec3f.get_len_squared(): Float {
        ret x * x + y * y + z * z
    }

    // Normalize the vector in place, makes the vector unit length
    // If the vector is zero, it will be set to (0, 0, 0)
    fun Vec3f.normalize() {
        let len: Float = this.get_len()
        if len == 0.0 {
            x = 0.0
            y = 0.0
            z = 0.0
            ret
        }
        x /= len
        y /= len
        z /= len
    }

    fun Vec3f.to_normalized(): Vec3f {
        let len = this.get_len()
        if len == 0.0 {
            ret Vec3f::new()
        }
        ret Vec3f @[x: x / len, y: y / len, z: z / len]
    }

    // Returns the angle in radians between this vector and another vector
    fun Vec3f.get_angle(other: Vec3f): Float {
        let dot_product = dot(other)
        let len_self = this.get_len()
        let len_other = other.get_len()
        if len_self == 0.0 || len_other == 0.0 {
            ret 0.0
        }
        let cos_angle = (dot_product / (len_self * len_other)).clamp(-1.0, 1.0)
        ret acos(cos_angle)
    }

    // Distance between two points
    fun Vec3f.distance(other: Vec3f): Float {
        let dx = x - other.x
        let dy = y - other.y
        let dz = z - other.z
        ret sqrt(dx * dx + dy * dy + dz * dz)
    }

    fun Vec3f.distance_squared(other: Vec3f): Float {
        let dx = x - other.x
        let dy = y - other.y
        let dz = z - other.z
        ret dx * dx + dy * dy + dz * dz
    }

    // Reflect the vector across a given axis
    fun Vec3f.reflect(axis: Vec3f) {
        // Normalize the axis vector
        let len = axis.get_len()
        if len == 0.0 {
            return // Cannot reflect against a zero-length vector
        }
        let normalized_axis = axis.to_normalized()

        // Calculate the dot product with the normalized axis
        let dot_product = this.dot(normalized_axis)

        // Reflect the vector
        x -= 2f * dot_product * normalized_axis.x
        y -= 2f * dot_product * normalized_axis.y
        z -= 2f * dot_product * normalized_axis.z
    }

    fun Vec3f.to_reflected(axis: Vec3f): Vec3f {
        // Normalize the axis vector
        let len = axis.get_len()
        if len == 0.0 {
            ret this // Cannot reflect against a zero-length vector
        }
        let normalized_axis = axis.to_normalized()

        // Calculate the dot product with the normalized axis
        let dot_product = this.dot(normalized_axis)

        // Reflect the vector
        ret Vec3f @[
            x: x - 2f * dot_product * normalized_axis.x,
            y: y - 2f * dot_product * normalized_axis.y,
            z: z - 2f * dot_product * normalized_axis.z,
        ]
    }

    // Numeric Operations

    fun Vec3f.negate() {
        x = -x
        y = -y
        z = -z
    }

    fun Vec3f.to_negated(): Vec3f {
        ret Vec3f @[x: -x, y: -y, z: -z]
    }

    // Linear Interpolation between two vectors
    fun Vec3f.lerp(other: Vec3f, t: Float): Vec3f {
        ret Vec3f @[x: x + (other.x - x) * t, y: y + (other.y - y) * t, z: z + (other.z - z) * t]
    }

    // Store in this vector the result of linear interpolation of two vectors
    fun Vec3f.set_to_lerp(one: Vec3f, other: Vec3f, t: Float) {
        x += (other.x - one.x) * t
        y += (other.y - one.y) * t
        z += (other.z - one.z) * t
    }

    fun Vec3f.min(other: Vec3f): Vec3f {
        ret Vec3f @[
            x: if x < other.x { x } else { other.x },
            y: if y < other.y { y } else { other.y },
            z: if z < other.z { z } else { other.z },
        ]
    }

    fun Vec3f.set_to_min(one: Vec3f, other: Vec3f) {
        x = if x < other.x { x } else { other.x }
        y = if y < other.y { y } else { other.y }
        z = if z < other.z { z } else { other.z }
    }

    fun Vec3f.max(other: Vec3f): Vec3f {
        ret Vec3f @[
            x: if x > other.x { x } else { other.x },
            y: if y > other.y { y } else { other.y },
            z: if z > other.z { z } else { other.z },
        ]
    }

    fun Vec3f.set_to_max(one: Vec3f, other: Vec3f) {
        x = if x > other.x { x } else { other.x }
        y = if y > other.y { y } else { other.y }
        z = if z > other.z { z } else { other.z }
    }

    // Proxy Operations

    fun Vec3f.abs() {
        x = x.abs()
        y = y.abs()
        z = z.abs()
    }

    fun Vec3f.to_abs(): Vec3f {
        ret Vec3f @[
            x: x.abs(),
            y: y.abs(),
            z: z.abs(),
        ]
    }

    fun Vec3f.floor() {
        x = x.floor()
        y = y.floor()
        z = z.floor()
    }

    fun Vec3f.to_floor(): Vec3f {
        ret Vec3f @[
            x: x.floor(),
            y: y.floor(),
            z: z.floor(),
        ]
    }

    fun Vec3f.ceil() {
        x = x.ceil()
        y = y.ceil()
        z = z.ceil()
    }

    fun Vec3f.to_ceil(): Vec3f {
        ret Vec3f @[
            x: x.ceil(),
            y: y.ceil(),
            z: z.ceil(),
        ]
    }

    fun Vec3f.round() {
        x = x.round()
        y = y.round()
        z = z.round()
    }

    fun Vec3f.to_round(): Vec3f {
        ret Vec3f @[
            x: x.round(),
            y: y.round(),
            z: z.round(),
        ]
    }

    fun Vec3f.trunc() {
        x = x.trunc()
        y = y.trunc()
        z = z.trunc()
    }

    fun Vec3f.to_trunc(): Vec3f {
        ret Vec3f @[
            x: x.trunc(),
            y: y.trunc(),
            z: z.trunc(),
        ]
    }

    // Checks

    fun Vec3f.is_finite(): Boolean {
        ret x.is_finite() && y.is_finite() && z.is_finite()
    }

    fun Vec3f.is_nan(): Boolean {
        ret x.is_nan() || y.is_nan() || z.is_nan()
    }

    fun Vec3f.is_zero(): Boolean {
        ret is_equal(0.0, 0.0, 0.0, epsilon)
    }

    fun Vec3f.is_equal(other: Vec3f, epsilon: Float): Boolean {
        ret (this.x - other.x).abs() < epsilon && (this.y - other.y).abs() < epsilon && (this.z - other.z).abs() < epsilon
    }

    fun Vec3f.is_equal(other_x: Float, other_y: Float, other_z: Float, epsilon: Float): Boolean {
        ret (this.x - other_x).abs() < epsilon && (this.y - other_y).abs() < epsilon && (this.z - other_z).abs() < epsilon
    }

    // Standard Operations

    fun Vec3f.to_vec2f(): Vec2f {
        ret Vec2f @[x: x, y: y]
    }

    fun Vec3f.to_vec3f(): Vec3f {
        ret Vec3f @[x: x, y: y, z: z]
    }

    fun Vec3f.get_hash(): Int {
        ret 31 * (31 * x.reinterpret_as_int() + y.reinterpret_as_int()) + z.reinterpret_as_int()
    }

    fun Vec3f.get_ordering(other: Vec3f): Ordering {
        if x < other.x || (x == other.x && y < other.y) || (x == other.x && y == other.y && z < other.z) {
            ret Ordering::Less
        }
        if x > other.x || (x == other.x && y > other.y) || (x == other.x && y == other.y && z > other.z) {
            ret Ordering::Greater
        }
        ret Ordering::Equals
    }

    fun Vec3f.to_string(): String {
        ret "Vec3f($x, $y, $z)"
    }
}
