
// This type represents a heap-allocated sequence of items of type `#Item`
// The implementation is a dynamic array that doubles in size when it runs out of space.
// The array stores pointers to the items, so the items are not owned by the list
//
// A list can be created using the `[...]` syntax:
// ```
// let my_list: List<Int> = [1, 2, 3]
// ```
// or using the `List::new()` function:
// ```
// let my_list = List::new<Int>()
// my_list[] = 1
// my_list[] = 2
// my_list[] = 3
// ```
struct List<#Item> {
    capacity: Int
    count: Int
    items: RawArray<Ptr<#Item>>
}

// Creates an empty list
fun List::new<#Item>(): List<#Item> {
    ret List<#Item> @[
        capacity: 0
        count: 0
        items: null_ptr<Ptr<#Item>>().to_raw_array()
    ]
}

// Creates an empty list with the given capacity pre-allocated
fun List::new<#Item>(capacity: Int): List<#Item> {
    ret List<#Item> @[
        capacity: capacity
        count: 0
        items: RawArray::new<Ptr<#Item>>(capacity)
    ]
}

// Makes sure the list has capacity for the requested number of items
fun List<#Item>.ensure_capacity(capacity: Int) {
    if capacity <= this.capacity {
       ret
    }

    let new_capacity = max(this.capacity * 2, 16)

    while new_capacity < capacity {
        new_capacity = new_capacity * 2
    }

    let new_items: RawArray<Ptr<#Item>> = RawArray::new<Ptr<#Item>>(new_capacity)

    if this.count > 0 {
        this.items.copy_into(new_items, this.capacity)
    }

    this.items = new_items
    this.capacity = new_capacity
}

// Adds the given item to the list
// Can be called using the `list[] = value` syntax:
// `my_list[] = 1` is equivalent to `my_list.add(1)`
//
fun List<#Item>.add(item: #Item) {
    // Expand list if needed
    this.ensure_capacity(this.count + 1)

    let item_ptr = this.items.get_ptr(this.count)
    item_ptr.write(ptr_of<#Item>(item))
    this.count += 1
}

// Adds the given item to the list `count` times
fun List<#Item>.add_multiple_times(item: #Item, count: Int) {
    // Expand list if needed
    this.ensure_capacity(this.count + count)

    // Write the item `count` times
    repeat count {
        this.items.get_ptr(this.count).write(ptr_of<#Item>(item))
        this.count += 1
    }
}

fun List<#Item>.add_all(other: List<#Item>) {
    // Expand list if needed
    this.ensure_capacity(this.count + other.count)

    // Copy the items from the other list
    repeat other.count {
        this.add(other[it]!!)
    }
}

// Get the item at the given index, if the index is out of bounds, returns `None`
// Can be called using the `list[index]` syntax:
// `my_list[0]` is equivalent to `my_list.get(0)`
//
fun List<#Item>.get(index: Int): Optional<#Item> {
    if index < 0 || index >= this.count {
        ret None<#Item>()
    }

    let item_ptr: Ptr<#Item> = this.items.get_ptr(index).read()
    ret Some(item_ptr.unsafe_as_ref())
}

// Set the item at the given index, if the index is out of bounds, the program crashes
// Can be called using the `list[index] = value` syntax:
// `my_list[0] = 1` is equivalent to `my_list.set(0, 1)`
fun List<#Item>.set(index: Int, item: #Item) {
    if index < 0 || index >= this.count {
        crash("Invalid list index: $index, len: ${this.count}")
    }

    let item_ptr = this.items.get_ptr(index)
    item_ptr.write(ptr_of<#Item>(item))
}

// Swaps the items at the given indexes
fun List<#Item>.swap(index1: Int, index2: Int) {
    let temp = this[index1]!!
    this[index1] = this[index2]!!
    this[index2] = temp
}

// Insert a new item at the given index, if the index is out of bounds, the program crashes
fun List<#Item>.insert(index: Int, value: #Item) {
    if index < 0 || index >= this.count {
        crash("Invalid list index: $index, len: ${this.count}")
    }

    // Expand list if needed
    this.ensure_capacity(this.count + 1)

    // Add a new item slot at the end
    this.count += 1

    // Move every item one place to the right
    repeat this.count - index {
        this.swap(this.count - it - 1, this.count - it)
    }

    // Finish by inserting the new item
    this[index] = value
}

// Creates a new list with the given item added at the end
fun List<#Item>.plus(item: #Item): List<#Item> {
    let result = this.to_list()
    result[] = item
    ret result
}

// Crea a new list with the given items added at the end
fun List<#Item>.plus(other: List<#Item>): List<#Item> {
    let result = this.to_list()

    repeat other.count {
        result[] = other[it]!!
    }

    ret result
}

// Removes the item at the given index, if the index is out of bounds, the program crashes
fun List<#Item>.remove_at(index: Int): Optional<#Item> {
    if index < 0 || index >= this.count {
        ret None()
    }

    let value = this[index]

    // Move every item one place to the left
    repeat this.count - index - 1 {
        this.swap(index + it, index + it + 1)
    }

    this.count -= 1
    ret value
}

// Removes the last item in the array
fun List<#Item>.remove_last(): Optional<#Item> {
    if count == 0 {
        ret None()
    }

    let item = this[count - 1]!!
    count -= 1
    ret Some(item)
}

// Removes the last item in the array
fun List<#Item>.remove_first(): Optional<#Item> {
    ret this.remove_at(0)
}

// Removes the given item from the list, if the item is not in the list, nothing happens
fun <#Item: GetOrdering> List<#Item>.remove_value(item: #Item) {
    let index = this.index_of(item)

    if index.is_some() {
        this.remove_at(index!!)!!
    }
}

fun List<#Item>.clear() {
    this.count = 0
}

fun List<#Item>.remove_all(predicate: (#Item) -> Boolean) {
    let i = 0

    while i < this.count {
        if predicate(this[i]!!) {
            this.remove_at(i)!!
        } else {
            i += 1
        }
    }
}

// Returns the index of the given item, if the item is not in the list, returns `None`
fun <#Item: GetOrdering> List<#Item>.index_of(item: #Item): Optional<Int> {
    repeat this.count {
        if this[it]!! == item {
            ret Some(it)
        }
    }

    ret None()
}

// Returns the size of the list, meaning the number of items it contains
// Deprecated, use property `.len` instead
fun List<#Item>.len(): Int {
    ret this.count
}

// Checks if the list is empty
fun List<#Item>.is_empty(): Boolean = this.count == 0

// Checks if the list is not empty
fun List<#Item>.is_not_empty(): Boolean = this.count != 0

// Checks if the list contains the given item
fun <#Item: GetOrdering> List<#Item>.contains(item: #Item): Boolean {
    ret this.index_of(item).is_some()
}

// Returns a new list where each item is replaced by the result of the given function
fun <#Item, #Other> List<#Item>.map(func: (#Item) -> #Other): List<#Other> {
    let result = List::new<#Other>(this.count)

    repeat this.count {
        result[] = func(this[it]!!)
    }

    ret result
}

// Each item is passed to the given function to get a new list, the result is a list will all the items
fun List<#Item>.flat_map(func: (#Item) -> List<#Other>): List<#Other> {
    let result = List::new<#Other>(this.count)

    repeat this.count {
        let items = func(this[it]!!)
        repeat items.count {
            result[] = items[it]!!
        }
    }

    ret result
}

// Returns a new list with only the items that match the given predicate
fun List<#Item>.filter(func: (#Item) -> Boolean): List<#Item> {
    let result = List::new<#Item>(this.count)

    repeat this.count {
        if func(this[it]!!) {
            result[] = this[it]!!
        }
    }

    ret result
}

// Given a list of lists, returns a new list with all the items in the sublists
fun List<List<#Item>>.flatten(): List<#Item> {
    let result = List::new<#Item>(this.count)

    repeat this.count {
        let items = this[it]!!
        repeat items.count {
            result[] = items[it]!!
        }
    }

    ret result
}

// Counts the number of items in the list that match the given predicate
fun List<#Item>.count(predicate: (#Item) -> Boolean): Int {
    let count = 0

    repeat this.count {
        if predicate(this[it]!!) {
            count += 1
        }
    }

    ret count
}

// Returns true if all the items in the list match the given predicate
// If the list is empty, returns true
fun List<#Item>.all(predicate: (#Item) -> Boolean): Boolean {
    repeat this.count {
        if !predicate(this[it]!!) {
            ret false
        }
    }

    ret true
}

// Returns true if any of the items in the list match the given predicate
// If the list is empty, returns false
fun List<#Item>.any(predicate: (#Item) -> Boolean): Boolean {
    repeat this.count {
        if predicate(this[it]!!) {
            ret true
        }
    }

    ret false
}

// Returns true if none of the items in the list match the given predicate
// If the list is empty, returns true
fun List<#Item>.none(predicate: (#Item) -> Boolean): Boolean {
    repeat this.count {
        if predicate(this[it]!!) {
            ret false
        }
    }

    ret true
}

// Returns the first item in the list, if the list is empty, returns `None`
fun List<#Item>.first(): Optional<#Item> {
    if this.is_empty() {
        ret None()
    }

    ret Some(this[0]!!)
}

// Returns a new list with all the items except the first one
fun List<#Item>.rest(): List<#Item> {
    let result = List::new<#Item>(this.count - 1)

    repeat this.count - 1 {
        result[] = this[it + 1]!!
    }

    ret result
}

// Returns the last item in the list, if the list is empty, returns `None`
fun List<#Item>.last(): Optional<#Item> {
    if this.is_empty() {
        ret None()
    }

    ret Some(this[this.count - 1]!!)
}

// Finds the first item in the list that matches the given predicate, if no item matches, returns `None`
fun List<#Item>.find(predicate: (#Item) -> Boolean): Optional<#Item> {
    repeat this.count {
        if predicate(this[it]!!) {
            ret Some(this[it]!!)
        }
    }

    ret None()
}

// Finds the index of the first item in the list that matches the given predicate, if no item matches, returns `None`
fun List<#Item>.find_index(predicate: (#Item) -> Boolean): Optional<Int> {
    repeat this.count {
        if predicate(this[it]!!) {
            ret Some(it)
        }
    }

    ret None()
}

// Generates a string with debug information about the list internals
fun List<#Item>.to_debug_string(): String {
    ret "List @[capacity: ${this.capacity}, len: ${this.count}, data: ${this.items}]"
}

// Converts the list to a string
fun <#Item: ToString> List<#Item>.to_string(): String {
    let str = "["

    repeat this.count {
        let item = this[it]!!.to_string()
        str = str.concat(item)

        if it != limit - 1 {
            str = str.concat(", ")
        }
    }


    ret str.concat("]")
}

// Iterates over the list and calls the given function for each item
// Can be called using the `list.for_each @{ ... }` syntax:
// ```
// [1, 2, 3].for_each @{ item: Int ->
//     println(item)
// }
// ```
fun List<#Item>.for_each(func: (#Item) -> Nothing) {
    repeat this.count {
        func(this[it]!!)
    }
}

// Iterates over the list and calls the given function for each item, passing the index as well
// Can be called using the `list.for_each_entry @{ ... }` syntax:
// ```
// [1, 2, 3].for_each_entry @{ index: Int, item: Int ->
//     println("Item at index $index is $item")
// }
fun List<#Item>.for_each_entry(func: (Int, #Item) -> Nothing) {
    repeat this.count {
        func(it, this[it]!!)
    }
}

// Concatenates all the items in the list into a string, separated by the given separator
fun <#Item: ToString> List<#Item>.join(separator: String): String {
    let str = ""

    repeat this.count {
        str = str.concat(this[it]!!.to_string())

        if it != this.count - 1 {
            str = str.concat(separator)
        }
    }

    ret str
}

// Creates a new list with items between the given start and end indexes
// If start or end are negative, they are counted from the end of the list
fun List<#Item>.to_sublist(start: Int, end: Int): List<#Item> {
    if end < 0 {
       end = this.count + end
    }
    if end >= this.count {
        end = this.count - 1
    }

    if start < 0 {
       start = this.count + start
    }
    if start < 0 {
        start = 0
    }

    let len = end - start
    if len <= 0 {
        ret List::new<#Item>()
    }

    let sublist = List::new<#Item>(len)
    let i: Int = start

    while i < end {
        sublist[] = this[i]!!
        i += 1
    }

    ret sublist
}

// Creates a copy of this list
fun List<#Item>.to_list(): List<#Item> {
    let list = List::new<#Item>(this.count)

    repeat this.count {
        list[] = this[it]!!
    }

    ret list
}

// Creates new list with all the items in reverse order
fun List<#Item>.to_reversed(): List<#Item> {
    let list = List::new<#Item>(this.count)

    repeat this.count {
        list[] = this[this.count - it - 1]!!
    }

    ret list
}

// Sorts the list in place, without allocating a new list
fun <#Item: GetOrdering> List<#Item>.sort_in_place() {
    // Already sorted
    if this.count <= 1 {
        ret
    }

    this.quicksort_in_place()
}

// Returns a sorted copy of the list
fun <#Item: GetOrdering> List<#Item>.to_sorted_list(): List<#Item> {
    let sorted = this.to_list()
    sorted.sort_in_place()
    ret sorted
}
