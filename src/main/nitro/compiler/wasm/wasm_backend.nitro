
struct WasmBackend {
    reporter: Reporter
    program: Program
    wasm_program: WasmProgram
    emitter: WasmEmitter
    final_types: FinalTypeContainer
    consts: List<WConst>
    const_id_provider: IdProvider
    function_indices: IdProvider
    import_indices: IdProvider
    final_struct_cache: Map<FinalType, FinalStruct>
    final_option_cache: Map<FinalType, FinalOption>
    already_visited: Map<MonomorphizationKey, Id>
    task_queue: ArrayDeque<CompilationTask>
    root_ctx: TypeMappingCtx
}

option CompilationTask {
    VisitFunction {
        key: MonomorphizationKey
        index: Id
        instance: NFunction
        ctx: TypeMappingCtx
    }
    VisitLambda {
        key: MonomorphizationKey
        index: Id
        lambda_index: Id
        instance: NLambda
        ctx: TypeMappingCtx
    }
    VisitConst {
        key: MonomorphizationKey
        index: Id
        instance: NConst
    }
}

fun WasmBackend::new(reporter: Reporter, program: Program): WasmBackend {
    let final_types = FinalTypeContainer::new()
    let back =  WasmBackend @[
        reporter,
        program,
        wasm_program: WasmProgram::new(),
        emitter: WasmEmitter::new(),
        final_types,
        consts: [],
        const_id_provider: IdProvider::new(),
        function_indices: IdProvider::new(),
        import_indices: IdProvider::new(),
        final_struct_cache: #[],
        final_option_cache: #[],
        task_queue: ArrayDeque::new(),
        already_visited: #[],
        root_ctx: TypeMappingCtx::new(final_types, Span::none())
    ]
    back.emitter.backend = Some(back)
    ret back
}

fun WasmBackend.compile_program() {
    let main: Optional<NFunction> = None()

    for f in program.functions {
        if f.full_name == "main" {
            main = Some(f)
            break
        }
    }

    if main.is_none() {
        crash("Main function not found!")
    }

    // Init memory
    let null_value: List<Byte> = [0x00.to_byte(), 0x00.to_byte(), 0x00.to_byte(), 0x00.to_byte()]
    wasm_program.memory.add_section(null_value, "Null value")

    // Allocate space for the memory management struct
    // capacity: Int, len: Int, data: Ptr<Byte>
    let memory = ByteWriter::new()
    memory.add_pad(0x00.to_byte(), 12)
    let memory_management_section = wasm_program.memory.add_section(memory.bytes, "Memory instance")

    // Do dead code elimination and monomorphization by traversing the program from the main function
    wasm_program.start = Some(visit_program(main!!))

    // Genera _start_main that initializes all the constants and calls the main function
    autogenerate_function_start_main()

    // Rewrite indices so imports come before regular functions
    // So `call` instructions use the correct index
    let function_ids_start = import_indices.last_id + 1
    function_indices.increment_all_existing_ids(function_ids_start)
    let last_function_id = function_indices.get_max_id()

    // Place lambda types at the end
    let next_type_id = last_function_id + 1
    for header in wasm_program.indirect_function_types {
        header.index.update(next_type_id)
        next_type_id += 1
    }

    // Generate code for functions
    for func in wasm_program.functions {
        if func.source is WFunctionSource::Function {
            let instance = (func.source as WFunctionSource::Function).instance
            let extern = instance.get_extern_annotation()

            if extern.is_some() && extern!!.autogenerate {
//                Log::debug("Auto-generating function ${extern!!.name}")
                compile_autogenerated_function(func.ctx, instance, func)
                continue
            }
        }

        func.bytecode.compile_code(func, this)
    }

    compile_function_start_main()

    // Add main function as export to be called by the runtime
    let start_main = wasm_program.functions.find @{ it -> it.name == "_start_main" }!!
    wasm_program.function_exports[] = WFunctionExport @[
        name: "_start_main",
        header: start_main.header,
    ]

    // Init the memory management struct
    // len: 0
    // data: start of not-yet-allocated memory
    // capacity: total size - allocated size
    let heap_start = WasmBackend::pad(wasm_program.memory.offset + 4)
    let capacity = wasm_program.memory.size - heap_start - 4 /* capacity of Array 'data' */
    memory.bytes.clear()
    memory.add_int_le(0)
    memory.add_int_le(heap_start)
    memory.add_int_le(capacity)

    let capacity_section = ByteWriter::new()
    capacity_section.add_int_le(capacity)
    wasm_program.memory.add_section(capacity_section.bytes, "Memory capacity")

    // Functions must in be ordered by index in the wasm binary
    wasm_program.functions.sort_in_place()

    // Finish the program
    emitter.emit_program(wasm_program)
}

fun WasmBackend.get_key_for_function(ctx: TypeMappingCtx, instance: NFunction): MonomorphizationKey {
    let params_final_types = []
    // This is here just to add 'span' as context for the error message, is a generic type cannot be resolved
    let sub_ctx = ctx.new_child(instance.span)

    for param in instance.header.parameters {
        params_final_types[] = sub_ctx.convert_type(param.param_type!!)
    }

    params_final_types[] = sub_ctx.convert_type(instance.header.return_type!!)

    let final_type = final_types.create_final_type(FinalTypeKind::Function @[instance], params_final_types)

    ret MonomorphizationKey @[
        name: instance.full_name,
        final_type,
    ]
}

fun WasmBackend.get_key_for_const(instance: NConst): MonomorphizationKey {
    let final_type = root_ctx.convert_type(instance.const_type!!)

    ret MonomorphizationKey @[
        name: instance.full_name,
        final_type,
    ]
}

fun WasmBackend.get_function_header(key: MonomorphizationKey, index: Id, is_import: Boolean): WFunctionHeader {
    // Function header
    let header = WFunctionHeader @[
        index,
        key,
        name: key.name,
        params: [],
        results: [],
    ]

    let params = key.final_type.type_params

    // Params
    repeat params.len - 1 {
        encode_final_type(params[it]!!, header.params, is_import)
    }

    // Results
    encode_final_type(params.last()!!, header.results, false)

    ret header
}

fun WasmBackend.get_indirect_function_header(final_type: FinalType): WFunctionHeader{
    let key = MonomorphizationKey @[name: "Function", final_type]
    let header = get_function_header(key, Id::uninitialized(), false)

    // Implicit `self` argument, used to store the indirect function pointer and upvalues
    header.params.add_first(WType::I32)
    wasm_program.indirect_function_types[] = header

    ret header
}

// Given a final type, i.e. List<Int>, store in [list] the wasm type (i32, f32, etc.) that
// will store a pointer to the value, for Int/Float/Long, store the type itself
fun WasmBackend.encode_final_type(final_type: FinalType, list: List<WType>, is_import: Boolean) {
    let size = get_size_of(final_type)

    // Zero size types
    if size == 0 {
        if is_import {
            list[] = WType::I32
        }
        ret
    }

    // Float
    if final_type.is_float() {
        list[] = WType::F32
        ret
    }

    // Long
    if final_type.is_long() {
        list[] = WType::I64
        ret
    }

    // Type for pointers, integers and types with size smaller than 4 bytes
    list[] = WType::I32
}

fun WasmBackend.encode_to_wasm_type(final_type: FinalType): WType {
    let result = []
    encode_final_type(final_type, result, false)

    if result.is_empty() {
        ret WType::I32
    }

    ret result.first()!!
}

fun WasmBackend.get_ref_size_of(final_type: FinalType): Int {
    when {
        final_type.kind is FinalTypeKind::Struct -> {
            let instance: NStruct = (final_type.kind as FinalTypeKind::Struct).instance

            ret when instance.full_name {
                "Never" -> 0
                "Nothing" -> 0
                "Byte" -> 1
                "Boolean" -> 1
                "Short" -> 2
                "Char" -> 4
                "Int" -> 4
                "Float" -> 4
                "Ptr" -> 4
                "Array" -> 4
                "Function" -> 4
                "Long" -> 8
                else -> 4
            }
        }
        final_type.kind is FinalTypeKind::Option -> {
            ret 4
        }
        final_type.kind is FinalTypeKind::Lambda -> {
            ret 4
        }
    }
    unreachable()
}

fun WasmBackend.get_size_of(final_type: FinalType): Int {
    when {
        final_type.kind is FinalTypeKind::Struct -> {
            let instance: NStruct = (final_type.kind as FinalTypeKind::Struct).instance

            ret when instance.full_name {
                "Never" -> 0
                "Nothing" -> 0
                "Byte" -> 1
                "Boolean" -> 1
                "Short" -> 2
                "Char" -> 4
                "Int" -> 4
                "Float" -> 4
                "Ptr" -> 4
                "Array" -> 4
                "Function" -> 4
                "Long" -> 8
                else -> get_final_struct(instance, final_type).size
            }
        }
        final_type.kind is FinalTypeKind::Option -> {
            let instance: NOption = (final_type.kind as FinalTypeKind::Option).instance
            ret get_final_option(instance, final_type).size
        }
        final_type.kind is FinalTypeKind::Lambda -> {
            let instance: NLambda = (final_type.kind as FinalTypeKind::Lambda).instance
            ret get_final_lambda(instance, final_type).size
        }
    }
    unreachable()
}

fun WasmBackend.get_final_struct(instance: NStruct, final_type: FinalType): FinalStruct {
    let opt = final_struct_cache[final_type]
    if opt.is_some() {
        ret opt!!
    }

    // Header for recursive type usage
    let final_struct = FinalStruct @[
        instance,
        final_type,
        fields: [],
        size: 0,
        parent_option: None()
    ]
    final_struct_cache[final_type] = final_struct

    let ctx = TypeMappingCtx::new(final_types, instance.span)
    ctx.add_generics(instance.generics, final_type.type_params)

    // Fields
    let field_offset = 0
    for field in instance.fields {
        let ty: Type = field.field_type!!
        let field_type = ctx.convert_type(ty)
        let size = get_ref_size_of(field_type)

        let final_field = FinalStructField @[
            name: field.name,
            final_type: field_type,
            size,
            offset: field_offset
        ]
        final_struct.fields[] = final_field
        field_offset += size
    }

    // Link to parent option
    if instance.parent.is_some() {
        let noption: NOption = instance.parent!!
        let kind = FinalTypeKind::Option @[instance: noption]
        let option_type = final_types.create_final_type(kind, final_type.type_params)

        let final_option = get_final_option(noption, option_type)
        final_struct.parent_option = Some(final_option)
    }

    // Size computation
    final_struct.size = 0
    for field in final_struct.fields {
        if field.size >= 4 {
            final_struct.size = WasmBackend::pad(final_struct.size)
        }
        final_struct.size += field.size
    }

    // Pad to 32 bit alignment
    if final_struct.size > 4 {
        final_struct.size = WasmBackend::pad(final_struct.size)
    }

    ret final_struct
}

fun WasmBackend.get_final_option(instance: NOption, final_type: FinalType): FinalOption {
    let opt = final_option_cache[final_type]
    if opt.is_some() {
        ret opt!!
    }

    // Header for recursive type usage
    let final_option = FinalOption @[
        instance,
        final_type,
        items: [],
        size: 0,
    ]
    final_option_cache[final_type] = final_option

    // Items
    for item in instance.items {
        let struct_def = item.def
        let option_item_type = final_types.create_final_type(
            FinalTypeKind::Struct @[instance: struct_def],
            final_type.type_params
        )
        final_option.items[] = get_final_struct(struct_def, option_item_type)
    }

    // Size computation
    final_option.size = 0
    for item in final_option.items {
        if item.size >= 4 {
            final_option.size = WasmBackend::pad(final_option.size)
        }
        final_option.size += item.size
    }

    // Pad to 32 bit alignment
    if final_option.size > 4 {
        final_option.size = WasmBackend::pad(final_option.size)
    }

    ret final_option
}

fun WasmBackend.get_final_lambda(instance: NLambda, final_type: FinalType): FinalLambda {
    let final_lambda = FinalLambda @[
        instance,
        final_type,
        size: 0,
    ]

    let ctx = instance.ctx!!

    // Size computation
    // header + function ptr + upvalues
    final_lambda.size = STRUCT_HEADER_SIZE + 4

    let code = instance.code

    for upvalue_id in code.upvalues {
        let nvar = code.find_var(upvalue_id)
        let final_type = ctx.convert_type(nvar.type_token!!.find_type())
        let size = get_ref_size_of(final_type)

        if size > 0 {
            final_lambda.size += 4
        }
    }

    ret final_lambda
}

// Store a string in the wasm memory and return the pointer to the string
fun WasmBackend.push_string(value: String): Int {
    // Make sure the string instance is aligned to 4 bytes
    wasm_program.memory.pad_to(4)

    // struct String {
    //     bytes_len: Int
    //     utf8_data: Array<Byte>
    //     hash_cache: Int
    // }
    let struct_size = STRUCT_HEADER_SIZE + INT_SIZE + PTR_SIZE + INT_SIZE
    let instance = ByteWriter::new()
    instance.add_pad(0x00.to_byte(), STRUCT_HEADER_SIZE)
    instance.add_int_le(value.bytes_len)
    instance.add_int_le(wasm_program.memory.offset + struct_size)
    instance.add_int_le(-1)

    let instance_sec = wasm_program.memory.add_section(instance.bytes, "String instance")

    let contents = ByteWriter::new()
    // Array length
    contents.add_int_le(value.bytes_len)
    // Array contents
    contents.add_string_utf8_without_size(value)

    wasm_program.memory.add_section(contents.bytes, "String contents")

    // Return the pointer to the string
    ret instance_sec.offset
}

fun WasmBackend.find_const(id: Id): WConst {
    for c in consts {
        if c.index == id {
            ret c
        }
    }

    crash("Const with id '$id' not found!")
}

fun WasmBackend::pad(size: Int): Int {
    // Using bit manipulation
    ret (size + 3) & (3.bitwise_not())
}
