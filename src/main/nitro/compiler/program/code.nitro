
struct NCode {
    parent_code: Optional<NCode>
    parent_inst: Optional<NInstId>
    parent_block: Optional<NBlock>
    parent_span: Optional<Span>
    inst: List<NInst>
    vars: List<NVar>
    last_id: NInstId
    last_var_id: NVarId
    root_block: NBlock
    current_block: NBlock
    block_stack: List<NBlock>
    last_block_id: NBlockId
    jumped_out_of_block: Boolean
    break_jumps: List<NInstId>
    continue_jumps: List<NInstId>
    last_expression: Optional<NInstId>
}

fun NCode::new(): NCode {
    let root_block = NBlock @[
        id: 0,
        parent: None(),
        is_jump_target: false,
        deferred_code: [],
        variable_stack: [],
    ]

    ret NCode @[
        parent_code: None(),
        parent_inst: None(),
        parent_block: None(),
        parent_span: None(),
        inst: [],
        vars: [],
        last_id: -1,
        last_var_id: -1,
        root_block,
        current_block: root_block,
        block_stack: [],
        last_block_id: 0,
        jumped_out_of_block: false,
        break_jumps: [],
        continue_jumps: [],
        last_expression: None(),
    ]
}

fun NCode.new_child(span: Span): NCode {
    let subcode = NCode::new()
    subcode.parent_code = Some(this)
    subcode.parent_inst = Some(last_id)
    subcode.parent_block = Some(current_block)
    subcode.parent_span = Some(span)
    ret subcode
}

fun NCode.enter_block(is_jump_target: Boolean) {
    block_stack[] = current_block
    last_block_id += 1
    current_block = NBlock @[
        id: last_block_id,
        parent: Some(current_block),
        is_jump_target,
        deferred_code: [],
        variable_stack: [],
    ]
}

fun NCode.exit_block() {
    if !jumped_out_of_block && current_block.deferred_code.is_not_empty() {
        for subcode in current_block.deferred_code.to_reversed() {
            this.merge(subcode)
        }
    }
    jumped_out_of_block = false
    current_block = block_stack.remove_last()!!
}

fun NCode.find(id: NInstId): Optional<NInst> {
    for i in this.inst {
        if i.id == id {
            ret Some(i)
        }
    }

    ret None()
}

fun NCode.execute_deferred_actions(block: NBlock, is_return: Boolean) {
    if block.deferred_code.is_not_empty() {
        for subcode in block.deferred_code.to_reversed() {
            this.merge(subcode)
        }
    }

    if block.parent.is_some() && (is_return || !block.is_jump_target) {
        execute_deferred_actions(block.parent!!, is_return)
    }
}

struct NCodeJumps {
    prev_break_jumps: List<NInstId>
    prev_continue_jumps: List<NInstId>
}

fun NCode.save_jump_points(): NCodeJumps {
    let prev_break_jumps = this.break_jumps
    let prev_continue_jumps = this.continue_jumps

    this.break_jumps = []
    this.continue_jumps = []

    ret NCodeJumps @[prev_break_jumps, prev_continue_jumps]
}

fun NCode.link_jump_points(break_block: NBlock, continue_block: NBlock) {
    // Link all continue and break jumps inside de loop
    for jump in this.break_jumps {
        let kind = this.find(jump)!!.kind as NInstKind::BlockJump
        kind.block = Some(break_block)
    }

    for jump in this.continue_jumps {
        let kind = this.find(jump)!!.kind as NInstKind::BlockJump
        kind.block = Some(continue_block)
    }
}

fun NCode.restore_jump_points(jumps: NCodeJumps) {
    this.break_jumps = jumps.prev_break_jumps
    this.continue_jumps = jumps.prev_continue_jumps
}

fun NCode.merge(other: NCode) {
    let mapping: Map<NInstId, NInstId> = #[]

    for i in other.inst {
        this.add_copy(i, mapping)
    }
}

fun NCode.to_string(): String {
    if inst.is_empty() {
        ret "{}"
    }

    let body_str = "{"

     for i in inst {
        let id = i.id.to_string()
        let line = ""
        line += " " * (5 - id.len - 1)
        line += "#"
        line += id
        line += " | "
        let prefix_len = line.len
        line += "  " * i.current_block.depth
        line += i.to_string().indent(prefix_len).trim()

        body_str += "\n"
        body_str += line
    }
    body_str += "\n  last-expression: $last_expression"
    body_str += "\n}"

    ret body_str
}