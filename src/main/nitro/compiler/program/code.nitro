
// NCode represents a block of code, which can be a function, a constant initialization or a lambda expression.
struct NCode {
    // Ref to the program containing all the codes
    program: Program
    // Unique identifier of the code
    id: Id
    // Container of the code, ie. the function, constant or lambda expression that contains this block of code
    container: NCodeContainer
    // Indicate that this block belong to the same context as the parent, for example, for code reordering, will be false for lambdas, defer, etc.
    is_transparent_child: Boolean
    // List of lambdas defined in this code
    lambdas: List<Id>
    // Id provider for new instructions
    inst_id_provider: IdProvider
    // List of instructions of the function/constant
    inst_list: List<NInst>
    // Id provider for new variables
    variable_id_provider: IdProvider
    // Declared variables, names can be duplicated
    vars_map: Map<NVarId, NVar>
    //  Id provider for new block
    block_id_provider: IdProvider
    // All the blocks defined in this code
    block_map: Map<NBlockId, NBlock>
    // Current block, assigned to new instructions
    current_block: NBlockId
    // Flag to indicate if the code has jumped out of the current block with a return/continue/break
    jumped_out_of_block: Boolean
    // List of jumps generated by break statements, to be updated at the end of a loop
    break_jumps: List<NInstId>
    // List of jumps generated by continue statements, to be updated at the end of a loop
    continue_jumps: List<NInstId>
    // Last executed expression, used to generate implicit return statements
    last_expression: Optional<NInstId>
    // Type of the receiver of the function, used to generate implicit self references
    receiver_type_token: Optional<TypeToken>
    // Expected type to be returned by the function
    return_type_token: Optional<TypeToken>
    // Cache of constants used in this code, constants depend on the location of the function inside the global scope,
    // In a block of code (function or constant) a given name will always refer to the same constant
    const_cache: Map<String, NConst>
}

fun NCode::new(program: Program, container: NCodeContainer): NCode {
    let block_id_provider = IdProvider::new()

    let code = NCode @[
        program,
        id: program.id_provider.next(),
        container,
        is_transparent_child: false,
        lambdas: [],
        inst_list: [],
        vars_map: #[],
        inst_id_provider: IdProvider::new(),
        variable_id_provider: IdProvider::new(),
        current_block: block_id_provider.next(),
        block_id_provider: block_id_provider,
        block_map: #[],
        jumped_out_of_block: false,
        break_jumps: [],
        continue_jumps: [],
        last_expression: None(),
        receiver_type_token: None(),
        return_type_token: None(),
        const_cache: #[],
    ]

    let root_block = NBlock @[
        id: code.current_block,
        parent_id: None(),
        is_jump_target: false,
        depth: 0,
        deferred_code: [],
    ]

    code.block_map[root_block.id] = root_block
    ret code
}

fun NCode.new_child_code(container: NCodeContainer, is_transparent_child: Boolean): NCode {
    let subcode = NCode::new(program, container)
    subcode.is_transparent_child = is_transparent_child
    subcode.variable_id_provider.seek_to(variable_id_provider)
    ret subcode
}

fun NCode.get_generics(): NGenericParams = container.generics

fun NCode.get_parent_code(): Optional<NCode> {
    if container is NCodeContainer::Lambda {
        let def = (container as NCodeContainer::Lambda).def

        if def.container is NCodeContainer::None {
            crash("Cannot get parent code of a lambda without container, lambda: $def, code: $this")
        }

        ret Some(def.container.code)
    }
    ret None()
}

fun NCode.get_span(): Span = container.span

fun NCode.enter_block(is_jump_target: Boolean) {
    let prev = find_block(current_block)
    let new_block = NBlock @[
        id: block_id_provider.next(),
        parent_id: Some(prev.id),
        is_jump_target,
        depth: prev.depth + 1,
        deferred_code: [],
    ]
    block_map[new_block.id] = new_block
    current_block = new_block.id
}

fun NCode.exit_block() {
    let block = find_block(current_block)
    if !jumped_out_of_block && block.deferred_code.is_not_empty() {
        for subcode in block.deferred_code.to_reversed() {
            // Avoid replacing the last expression, since deferred code cannot be used for the implicit return
            let prev_last_expression = this.last_expression
            this.merge(subcode)
            this.last_expression = prev_last_expression
        }
    }
    jumped_out_of_block = false
    current_block = block.parent_id!!
}

fun NCode.find_inst(id: NInstId): NInst {
    for i in inst_list {
        if i.id == id {
            ret i
        }
    }

    crash("Instruction not found: #$id at $this")
}

fun NCode.find_inst_type_token(id: NInstId): Optional<TypeToken> {
    for i in inst_list {
        if i.id == id {
            if i.type_token.is_some() {
                ret Some(i.type_token!!)
            }
            ret None()
        }
    }

    ret None()
}

fun NCode.index_of_inst(id: NInstId): Optional<Int> {
    let current = 0
    for i in inst_list {
        if i.id == id {
            ret Some(current)
        }
        current += 1
    }

    ret None()
}

fun NCode.find_block(id: NBlockId): NBlock {
    let opt = block_map[id]
    if opt.is_some() {
        ret opt!!
    }

    if is_transparent_child {
        ret parent_code!!.find_block(id)
    }

    crash("Block not found: #$id at $this")
}

fun NCode.find_var(id: NVarId): NVar {
    let opt = vars_map[id]
    if opt.is_some() {
        ret opt!!
    }

    if parent_code.is_some() {
        ret parent_code!!.find_var(id)
    }

    crash("Variable not found: #$id at $this")
}

fun NCode.find_receiver_type(): Optional<Type> {
    if receiver_type_token.is_some() {
        ret Some(receiver_type_token!!.find_type())
    }

    if parent_code.is_some() {
        ret parent_code!!.find_receiver_type()
    }

    ret None()
}

fun NCode.get_upvalues(): List<NVarId> {
    let upvalues = []

    for inst in inst_list {
        if inst.kind is NInstKind::LetUpvalue {
            let kind = inst.kind as NInstKind::LetUpvalue
            upvalues[] = kind.variable
        }
    }

    ret upvalues
}

fun NCode.execute_deferred_actions(block_id: NBlockId, is_return: Boolean) {
    let block = find_block(block_id)

    if block.deferred_code.is_not_empty() {
        for subcode in block.deferred_code.to_reversed() {
            // Avoid replacing the last expression, since deferred code cannot be used for the implicit return
            let prev_last_expression = this.last_expression
            this.merge(subcode)
            this.last_expression = prev_last_expression
        }
    }

    if block.parent_id.is_none() && is_transparent_child {
        if container is NCodeContainer::Lambda {
            let def = (container as NCodeContainer::Lambda).def
            execute_deferred_actions(def.parent_block, is_return)
        }
    }

    if block.parent_id.is_some() && (is_return || !block.is_jump_target) {
        execute_deferred_actions(block.parent_id!!, is_return)
    }
}

struct NCodeJumps {
    prev_break_jumps: List<NInstId>
    prev_continue_jumps: List<NInstId>
}

fun NCode.save_jump_points(): NCodeJumps {
    let prev_break_jumps = this.break_jumps
    let prev_continue_jumps = this.continue_jumps

    this.break_jumps = []
    this.continue_jumps = []

    ret NCodeJumps @[prev_break_jumps, prev_continue_jumps]
}

fun NCode.link_jump_points(break_block: NBlockId, continue_block: NBlockId) {
    // Link all continue and break jumps inside de loop
    for jump in break_jumps {
        let kind = find_inst(jump).kind as NInstKind::BlockJump
        kind.block = Some(break_block)
    }

    for jump in continue_jumps {
        let kind = find_inst(jump).kind as NInstKind::BlockJump
        kind.block = Some(continue_block)
    }
}

fun NCode.restore_jump_points(jumps: NCodeJumps) {
    check_jump_points()

    this.break_jumps = jumps.prev_break_jumps
    this.continue_jumps = jumps.prev_continue_jumps
}

fun NCode.check_jump_points() {
    // Check all jumps are linked
    for jump in break_jumps {
        let kind = find_inst(jump).kind as NInstKind::BlockJump
        if kind.block.is_none() {
            crash("Jump not linked: 'break' #$jump\n$this")
        }
    }

    for jump in continue_jumps {
        let kind = find_inst(jump).kind as NInstKind::BlockJump
        if kind.block.is_none() {
            crash("Jump not linked: 'continue' #$jump\n$this")
        }
    }
}

fun NCode.calculate_back_references() {
    for inst in inst_list {
        inst.back_references.clear()
    }

    for inst in inst_list {
        when  {
            inst.kind is NInstKind::IfStart -> {
                let kind = inst.kind as NInstKind::IfStart
                let dep = find_inst(kind.condition)
                dep.back_references[] = inst.id
            }
            inst.kind is NInstKind::IfChoose -> {
                let kind = inst.kind as NInstKind::IfChoose
                let dep = find_inst(kind.condition)
                dep.back_references[] = inst.id

                let dep2 = find_inst(kind.if_true)
                dep2.back_references[] = inst.id

                let dep3 = find_inst(kind.if_false)
                dep3.back_references[] = inst.id
            }
            inst.kind is NInstKind::FunCall -> {
                let kind = inst.kind as NInstKind::FunCall
                for id in kind.arguments {
                    let dep = find_inst(id)
                    dep.back_references[] = inst.id
                }
            }
            inst.kind is NInstKind::Return -> {
                let kind = inst.kind as NInstKind::Return
                let dep = find_inst(kind.value)
                dep.back_references[] = inst.id
            }
            inst.kind is NInstKind::IsType -> {
                let kind = inst.kind as NInstKind::IsType
                let dep = find_inst(kind.value)
                dep.back_references[] = inst.id
            }
            inst.kind is NInstKind::AsType -> {
                let kind = inst.kind as NInstKind::AsType
                let dep = find_inst(kind.value)
                dep.back_references[] = inst.id
            }
            inst.kind is NInstKind::StoreVar -> {
                let kind = inst.kind as NInstKind::StoreVar
                let dep = find_inst(kind.value)
                dep.back_references[] = inst.id
            }
            inst.kind is NInstKind::LoadField -> {
                let kind = inst.kind as NInstKind::LoadField
                let dep = find_inst(kind.instance)
                dep.back_references[] = inst.id
            }
            inst.kind is NInstKind::StoreField -> {
                let kind = inst.kind as NInstKind::StoreField
                let dep = find_inst(kind.instance)
                dep.back_references[] = inst.id
                let dep2 = find_inst(kind.value)
                dep2.back_references[] = inst.id
            }
            inst.kind is NInstKind::Link -> {
                let kind = inst.kind as NInstKind::Link
                let dep = find_inst(kind.linked_id)
                dep.back_references[] = inst.id
            }
            else -> {}
        }
    }
}

fun NCode.to_string(): String {
    if inst_list.is_empty() && vars_map.is_empty() && block_map.is_empty() {
        ret "{}"
    }

    let body_str = StringBuilder::new()

    body_str[] = "{"
    body_str[] = "\n  id: $id"
    body_str[] = "\n  container: $container"
    body_str[] = "\n  span: ${this.span}"
    body_str[] = "\n  generics: ${generics}"
    body_str[] = "\n  vars: ["
    for variable in vars_map.values_to_list() {
        body_str[] = variable.name
        if variable.type_token.is_some() {
            body_str[] = ": "
            body_str[] = variable.type_token.to_string()
        }
        body_str[] = ","
    }
    body_str[] = "]"

    for i in inst_list {
        let id = i.id.to_string()
        let line = StringBuilder::new()
        line[] = " " * (5 - id.bytes_len - 1)
        line[] = "#"
        line[] = id
        line[] = " | "
        let prefix_len = line.bytes_len
        line[] = "  " * find_block(i.block).depth
        line[] = i.to_string(this).indent(prefix_len).trim()

        if i.type_token.is_some() {
            if !i.type_token!!.find_type().is_nothing() {
                line[] = " -> "
                line[] = i.type_token!!.to_string()
            }
        } else {
            line[] = " -> "
            line[] = "<not set>"
        }

        body_str[] = "\n"
        body_str[] = line
    }

    body_str[] = "\n  last_expression: $last_expression"
    body_str[] = "\n  receiver_type_token: $receiver_type_token"
    body_str[] = "\n  return_type_token: $return_type_token"
    body_str[] = "\n  lambdas: ${lambdas}"
    body_str[] = "\n}"

    ret body_str.to_string()
}

fun ByteBuffer.serialize_code(code: NCode) {
    let frame = start_frame(true)
    serialize_id(code.id)
    serialize_code_container(code.container)
    write_boolean(code.is_transparent_child)
    serialize_id_provider(code.inst_id_provider)
    serialize_id_provider(code.variable_id_provider)
    serialize_id_provider(code.block_id_provider)
    serialize_id(code.current_block)
    write_boolean(code.jumped_out_of_block)
    write_int(code.break_jumps.len)
    for jump in code.break_jumps {
        serialize_id(jump)
    }
    write_int(code.continue_jumps.len)
    for jump in code.continue_jumps {
        serialize_id(jump)
    }
    write_boolean(code.last_expression.is_some())
    if code.last_expression.is_some() {
        serialize_id(code.last_expression!!)
    }
    write_int(code.vars_map.len)
    for entry in code.vars_map {
        serialize_var(entry.second)
    }
    write_int(code.block_map.len)
    for entry in code.block_map {
        serialize_block(entry.second)
    }
    write_int(code.inst_list.len)
    for inst in code.inst_list {
        serialize_inst(inst)
    }
    write_int(code.lambdas.len)
    for lambda_id in code.lambdas {
        serialize_id(lambda_id)
    }
    end_frame(frame)
}

fun ByteBuffer.deserialize_code(program: Program): NCode {
    let frame = start_frame(false)
    let id = deserialize_id()
    let container = deserialize_code_container(program)
    let is_transparent_child = read_boolean()
    let lambdas = []
    let inst_id_provider = deserialize_id_provider()
    let inst_list = []
    let variable_id_provider = deserialize_id_provider()
    let vars_map = #[]
    let block_id_provider = deserialize_id_provider()
    let block_map = #[]
    let current_block = deserialize_id()
    let jumped_out_of_block = read_boolean()
    let break_jumps = []
    repeat read_int() {
        break_jumps[] = deserialize_id()
    }
    let continue_jumps = []
    repeat read_int() {
        continue_jumps[] = deserialize_id()
    }
    let last_expression = None()
    if read_boolean() {
        last_expression = Some(deserialize_id())
    }
    let value = NCode @[
        program,
        id,
        container,
        is_transparent_child,
        lambdas,
        inst_id_provider,
        inst_list,
        variable_id_provider,
        vars_map,
        block_id_provider,
        block_map,
        current_block,
        jumped_out_of_block,
        break_jumps,
        continue_jumps,
        last_expression,
        const_cache: #[],
        receiver_type_token: None(),
        return_type_token: None(),
    ]

    repeat read_int() {
        let variable = deserialize_var()
        vars_map[variable.id] = variable
    }

    repeat read_int() {
        let block = deserialize_block(value)
        block_map[block.id] = block
    }

    repeat read_int() {
        inst_list[] = deserialize_inst(value)
    }

    repeat read_int() {
        lambdas[] = deserialize_id()
    }

    end_frame(frame)
    ret value
}
