include "./includes.nitro"

// TODO run inline tests
// TODO generate extern types for typescript

let VERSION: String = "0.0.16"

struct ProgramArgs {
    input: String,
    output: String,
    target: String,
    dump_ir: Boolean,
    cache_dir: Optional<String>,
    module_paths: Map<String, String>,
    core_path: Optional<String>,
    no_core_lib: Boolean,
    verbose: Boolean,
    generate_dwarf: Boolean,
    compilation_flags: Map<String, Boolean>,
    dump_wat: Boolean,
}

fun ProgramArgs::new(): ProgramArgs {
    ret ProgramArgs @[
        input: "",
        output: "output.wasm",
        target: "wasm32-wasi",
        dump_ir: false,
        cache_dir: None(),
        module_paths: #[],
        core_path: None(),
        no_core_lib: false,
        verbose: false,
        dump_wat: false,
        generate_dwarf: false,
        compilation_flags: #[],
    ]
}

fun main() {
    let args = runtime::get_program_args()

    let i = 0
    let program_path = args[i]!!
    i += 1

    let prog = ProgramArgs::new()
    let dump_allocator = false

    // Process args
    while i < args.len {
        let arg = args[i]!!

        if arg == "--help" || arg == "-h" {
            Log::info("Usage: compiler <input> -o <output> --cache-dir <cache>")
            Log::info("Options:")
            Log::info(" -h, --help              Show this help message")
            Log::info(" -v, --version           Show version")
            Log::info(" -o, --output            Output file")
            Log::info(" -m, --mod               Module definitions")
            Log::info(" -t, --target            Target platform, wasm-wasi, wasm-js")
            Log::info(" -g                      Generate DWARF debug info (incomplete)")
            Log::info(" --flag                  Toggle a @CompilationFlag constant")
            Log::info(" --dump-ir               Dump IR")
            Log::info(" --dump-wasm-emitter     Dump Wasm emitter")
            Log::info(" --cache-dir             Cache directory")
            Log::info(" --dump-allocator        Dump allocator internal state")
            Log::info(" --no-core               Disable core library")
            Log::info(" --core-path             Path to the core library")
            Log::info(" --verbose               Verbose output")
            ret
        }

        if arg == "--version" || arg == "-v" {
            let v = VERSION
            Log::info("Nitro Compiler v$v")
            ret
        }

        if arg == "--cache-dir" {
            i += 1

            if i >= args.len {
                Log::error("Missing cache directory")
                ret
            }

            let cache_dir = args[i]!!
            i += 1
            prog.cache_dir = Some(cache_dir)
            continue
        }

        if arg == "--target" {
            i += 1

            if i >= args.len {
                Log::error("Missing target parameter")
                ret
            }

            let target = args[i]!!
            i += 1

            when target {
                "wasm32-wasi" -> {
                    prog.target = "wasm32-wasi"
                }
                "wasm32-wasi-unknown" -> {
                    prog.target = "wasm32-wasi"
                }
                "wasm32-wasip1" -> {
                    prog.target = "wasm32-wasi"
                }
                "wasm32-wasip1-unknown" -> {
                    prog.target = "wasm32-wasi"
                }
                "wasm32-js" -> {
                    prog.target = "wasm32-js"
                }
                "wasm32-js-unknown" -> {
                    prog.target = "wasm32-js"
                }
                else -> {
                    Log::error("Invalid target: $target")
                    Log::info("Valid targets: wasm, js")
                    ret
                }
            }
            continue
        }

        if arg == "--dump-ir" {
            prog.dump_ir = true
            i += 1
            continue
        }

        if arg == "--dump-wasm-emitter" {
            prog.dump_wat = true
            i += 1
            continue
        }

        if arg == "--dump-allocator" {
            dump_allocator = true
            i += 1
            continue
        }

        if arg == "--no-core" {
            prog.no_core_lib = true
            i += 1
            continue
        }

        if arg == "--verbose" {
            prog.verbose = true
            i += 1
            continue
        }

        if arg == "-g" {
            prog.generate_dwarf = true
            i += 1
            continue
        }

        if arg == "--flag" {
            i += 1

            if i >= args.len {
                Log::error("Missing flags definitions")
                Log::info("Help: --flag <name>=<value>")
                Log::info("e.g. --flag enable-rc-gc=true")
                ret
            }

            let flag_def = args[i]!!
            i += 1

            let split = flag_def.index_of("=")
            if split.is_none() {
                Log::error("Incorrect flags definitions")
                Log::info("Help: --flag <name>=<value>")
                Log::info("e.g. --flag enable-rc-gc=true")
                ret
            }
            let name = flag_def.substring(0, split!!)

            if name == "" {
                Log::error("Invalid flag name: '$name'")
                Log::info("Help: --flag <name>=<value>")
                Log::info("e.g. --flag enable-rc-gc=true")
                ret
            }

            let str_value = flag_def.substring(split!! + 1, flag_def.len)
            let value: Boolean = when str_value {
                "true" -> true
                "1" -> true
                "false" -> false
                "0" -> false
                else -> {
                    Log::error("Invalid flag value: '$str_value'")
                    Log::info("Help: --flag <name>=<value>")
                    Log::info("e.g. --flag enable-rc-gc=true")
                    ret
                }
            }

            prog.compilation_flags[name] = value
            i += 1
            continue
        }

        if arg == "--core-path" {
            i += 1

            if i >= args.len {
                Log::error("Missing core path")
                ret
            }

            let core_path = args[i]!!
            i += 1

            prog.core_path = Some(core_path)
            continue
        }

        if arg == "-m" || arg == "--mod" {
            i += 1

            if i >= args.len {
                Log::error("Missing module definitions")
                Log::info("Help: --mod <name>:<path>")
                Log::info("e.g. --mod math:./math.nitro")
                ret
            }

            let mod_def = args[i]!!
            i += 1

            let split = mod_def.index_of(":")
            if split.is_none() {
                Log::error("Invalid module definition: $mod_def")
                Log::info("Help: --mod <name>:<path>")
                Log::info("e.g. --mod math:./math.nitro")
                ret
            }
            let name = mod_def.substring(0, split!!)
            let path = mod_def.substring(split!! + 1, mod_def.len)

            if name == "" || path == "" {
                Log::error("Invalid module definition: $mod_def")
                Log::info("Help: --mod <name>:<path>")
                Log::info("e.g. --mod math:./math.nitro")
                ret
            }

            prog.module_paths[name] = path
            continue
        }

        if arg == "-o" || arg == "--output" {
            i += 1

            if i >= args.len {
                Log::error("Missing output file")
                ret
            }

            let output = args[i]!!
            i += 1
            prog.output = output
            continue
        }

        if prog.input == "" {
            i += 1
            prog.input = arg
            continue
        }

        Log::error("Unknown argument: $arg")
        ret
    }

    // Validate args
    if prog.input == "" {
        Log::error("Missing input file")
        Log::info("Help: create a main.nitro file and run 'nitro main.nitro'")
        Log::info("Example main.nitro:")
        Log::info("```nitro")
        Log::info("fun main() {")
        Log::info("    println(\"Hello, World!\")")
        Log::info("}")
        Log::info("```")
        ret
    }

    let input_path = FilePath::new(prog.input)
    let meta = input_path.get_metadata()

    if meta.is_err() {
        Log::error("Error reading input file '${prog.input}': $meta")
        ret
    }

    if meta!!.file_type != FileType::RegularFile {
        Log::error("Input is not a regular file '${prog.input}'")
        ret
    }

    if meta!!.size == 0L {
        Log::error("Input file is empty '${prog.input}'")
        ret
    }

    if prog.verbose {
        Log::info("Compiling ${prog.input} to ${prog.output}")
    }

    // Perform compilation
    if !CompilationUnit::compile_all(prog) {
        runtime::exit(1)
    }

    if dump_allocator {
        global_allocator.debug()
    }
}
