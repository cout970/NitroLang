// Statements
// E.g. {}
// E.g. { ret 0 }
// statementBlock
//     : LBRACE NL* statement (NL+ statement)* NL* RBRACE
//     | LBRACE NL* RBRACE
//     ;

// statement
//     : statementChoice ;

// statementChoice
//     : letStatement
//     | ifStatement
//     | forStatement
//     | repeatStatement
//     | whileStatement
//     | loopStatement
//     | whenExpr
//     | deferStatement
//     | expressionStatement
//     | foreignBlockStatement
//     | testDefinition
//     ;

// Read a block of statements
fun Parser.read_statement_block(code: NCode): Parsed<Nothing> {
    skip_token_kind(TokenKind::LBRACE)?
    skip_nl()

    ret read_statement_block_end(code)
}

fun Parser.read_statement_block_end(code: NCode): Parsed<Nothing> {
    if lexer.current_token_kind == TokenKind::RBRACE {
        skip_token_kind(TokenKind::RBRACE)?
        ret found(nothing)
    }

    loop {
        if lexer.current_token_kind == TokenKind::RBRACE {
            break
        }
        read_statement(code)?

        if lexer.current_token_kind != TokenKind::NL {
            break
        }

        skip_nl()
    }

    skip_token_kind(TokenKind::RBRACE)?

    ret found(nothing)
}

// Read a block of statements and make sure there is an expression at the end
fun Parser.read_statement_block_expr(code: NCode): Parsed<Nothing> {
    let span = lexer.current_token_span
    read_statement_block(code)?

    if code.last_expression.is_none() {
        let inst = code.add_lit_nothing(span)
        code.last_expression = Some(inst.id)
    }

    ret found(nothing)
}

fun Parser.read_statement(code: NCode): Parsed<Nothing> {
    let prev_last_expression = code.last_expression
    code.last_expression = None()

    when lexer.current_token_kind {
        TokenKind::DEFER -> {
            read_defer_statement(code)?
            // Prevent this statement from affecting the last expression
            code.last_expression = prev_last_expression
        }
        TokenKind::FOR -> read_for_statement(code)?
        TokenKind::FOREIGN_BLOCK -> {
            read_foreign_block_statement(code)?
            // Prevent this statement from affecting the last expression
            code.last_expression = prev_last_expression
        }
        TokenKind::IF -> read_if_statement(code)?
        TokenKind::LET -> read_let_statement(code)?
        TokenKind::REPEAT -> read_repeat_statement(code)?
        TokenKind::WHILE -> read_while_statement(code)?
        TokenKind::LOOP -> read_loop_statement(code)?
        TokenKind::WHEN -> read_when_statement(code)?
        TokenKind::TEST -> {
            read_test_definition([])?
            // Prevent this statement from affecting the last expression
            code.last_expression = prev_last_expression
        }
        else -> {
            let expr = read_expression(code)?
            // Update last_expression, so at the end of the block we know the last expression
            code.last_expression = Some(expr)
        }
    }
    ret found(nothing)
}