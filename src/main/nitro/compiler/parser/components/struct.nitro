
// structDefinition
//      : STRUCT upperName typeParamsDef? NL* structBody;
//
// E.g. { a: Int, b: Int }
// structBody
//      : LBRACE NL* (structField (commaOrNl structField)* COMMA?)? NL* RBRACE ;
//
// E.g. value: Int,
// structField
//      : INTERNAL? anyName COLON typeUsage;

struct NStruct {
    annotations: List<NAnnotation>
    name: String
    fields: List<NStructField>
    type_params: List<NTypeParameterDefinition>
}

struct NStructField {
    name: String
    type_usage: NTypeUsage
}

fun NStruct.to_string(): String {
    let prefix = ""

    for a in this.annotations {
        prefix = "$prefix$a\n"
    }

    let fields_str = "{ "
    for field in fields {
        fields_str += "${field.name}: ${field.type_usage}, "
    }
    fields_str = fields_str + "}"

    ret "${prefix}struct $name $fields_str"
}

fun Parser.read_struct_definition(annotations: List<NAnnotation>): Parsed<Nothing> {
    skip_token_kind(TokenKind::STRUCT)?
    let name = read_upper_name()?

    let def = NStruct @[
        annotations,
        name,
        type_params: [],
        fields: [],
    ]
    program.structs[] = def

    def.type_params = read_type_params_def()?

    skip_nl()
    skip_token_kind(TokenKind::LBRACE)?
    skip_nl()
    read_until(TokenKind::RBRACE) @{
        def.fields[] = read_struct_field()?
        skip_comma_or_nl()
    }?

    return found(nothing)
}

fun Parser.read_struct_field(): Parsed<NStructField> {
    ignore_token_kind(TokenKind::INTERNAL)
    let name = read_name()?

    skip_token_kind(TokenKind::COLON)?

    let type_usage = read_type_usage()?

    return found(NStructField @[name, type_usage])
}