
// E.g. let pi: Float = 3.14
// constDefinition
//     : LET modulePath? anyName COLON typeUsage ASSIGN NL* expression ;

fun Parser.read_const_definition(annotations: List<NAnnotation>): Parsed<Nothing> {
    skip_token_kind(TokenKind::LET)?
    let span = lexer.current_token_span
    let path = read_declaration_module_path()?
    let name = read_name()?

    let type_usage = if lexer.current_token_kind == TokenKind::ASSIGN {
        report_error("Constants must have a type defined", span)
        NTypeUsage::simple(span, "Nothing")
    } else {
        skip_token_kind(TokenKind::COLON)?
        read_type_usage()?
    }

    let def = NConst @[
        id: id_provider.next(),
        span,
        annotations,
        path,
        name,
        type_usage,
        const_type: None(),
        code: NCode::new(span, Path::join(path, name)),
        full_name_cache: None(),
    ]
    program.consts[] = def
    declare_name(Path::join(def.path, def.name), def.span)

    skip_token_kind(TokenKind::ASSIGN)?
    skip_nl()

    let expr = read_expression(def.code)?
    def.code.add_return(span, expr)

    ret found(nothing)
}
