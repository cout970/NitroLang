
// E.g. enum Direction { Up, Down, Left, Right, Front, Back }
// enumDefinition
//     : ENUM modulePath? anyName NL* LBRACE NL* enumFields? NL* enumValue (commaOrNl enumValue)* COMMA? NL* RBRACE
//     | ENUM modulePath? anyName NL* LBRACE NL* enumValue (commaOrNl enumValue)* COMMA? NL* enumFields? NL* RBRACE
//     ;

// enumFields
//     : enumField (commaOrNl enumField)* COMMA? ;

// E.g. let name: String = "John"
// enumField
//     : LET anyName COLON typeUsage ;

// E.g. Up, Down, Left, Right, Front, Back
// E.g. Red $[rgb: 0xFF0000], Green $[rgb: 0x00FF00], Blue $[rgb: 0x0000FF
// enumValue
//     : anyName
//     | anyName STRUCT_START NL* (enumValueInit (commaOrNl enumValueInit)* COMMA?)? NL* RBRACKET
//     ;

// E.g. rgb: 0xFF0000
// enumValueInit
//     : anyName COLON NL* expression ;

struct NEnumEntry {
    span: Span
    name: String
    variant: Int
    value_init: List<NEnumValueInit>
}

struct NEnumValueInit {
    field_name: String
    code: NCode
}

fun Parser.read_enum_definition(annotations: List<NAnnotation>): Parsed<Nothing> {
    // Enums are desugared into a struct and a module with the same name
    // enum Direction {
    //    Up      @[name: "up"]
    //    Down    @[name: "down"]
    //    Left    @[name: "left"]
    //    Right   @[name: "right"]
    //    Front   @[name: "front"]
    //    Back    @[name: "back"]
    //
    //    let name: String
    // }
    // ---
    // struct Direction {
    //    let name: String
    // }
    // mod Direction {
    //    let Up: Direction = Direction @[name: "up"]
    //    let Down: Direction = Direction @[name: "down"]
    //    let Left: Direction = Direction @[name: "left"]
    //    let Right: Direction = Direction @[name: "right"]
    //    let Front: Direction = Direction @[name: "front"]
    //    let Back: Direction = Direction @[name: "back"]
    //
    //    // Autogenerated:
    //    fun values(): List<Direction> = #[Up, Down, Left, Right, Front, Back]
    //    fun from_variant(index: Int): Optional<Direction> {}
    //    fun Direction.to_string(): String {}
    // }
    skip_token_kind(TokenKind::ENUM)?
    let span = lexer.current_token_span
    let path = read_module_path()?
    let name = read_name()?
    skip_nl()

    skip_token_kind(TokenKind::LBRACE)?
    skip_nl()

    let def = NStruct @[
        span,
        annotations,
        path,
        name,
        type_params: [],
        fields: [],
    ]
    program.structs[] = def

    def.fields[] = NStructField @[
        name: VARIANT_FIELD_NAME,
        type_usage: NTypeUsage::simple("Int"),
    ]

    let module_def = NModule @[
        span,
        annotations,
        path,
        name,
    ]
    program.modules[] = module_def
    let module_path = if path.is_empty() { name } else { "$path::$name" }

    // enumFields
    while lexer.current_token_kind == TokenKind::LET {
        skip_token_kind(TokenKind::LET)?
        let field_name = read_name()?
        skip_token_kind(TokenKind::COLON)?
        let type_usage = read_type_usage()?

        def.fields[] = NStructField @[
            name: field_name,
            type_usage,
        ]

        skip_nl()
    }

    // enumValues
    let enum_entries: List<NEnumEntry> = []
    let variant = 0

    loop {
        if lexer.current_token_kind == TokenKind::LET || lexer.current_token_kind == TokenKind::RBRACE {
            break
        }

        let span = lexer.current_token_span
        let entry_name = read_name()?
        let entry = NEnumEntry @[
            span,
            name: entry_name,
            variant,
            value_init: [],
        ]
        variant += 1
        enum_entries[] = entry

        if lexer.current_token_kind == TokenKind::STRUCT_START {
            skip_token_kind(TokenKind::STRUCT_START)?
            skip_nl()

            read_until_delimited(TokenKind::RBRACKET) @{
                let field_name = read_name()?
                skip_token_kind(TokenKind::COLON)?
                skip_nl()
                let code = NCode::new()
                read_expression(code)?

                entry.value_init[] = NEnumValueInit @[
                    field_name,
                    code,
                ]
                ret found(nothing)
            }?
        }

        if lexer.current_token_kind == TokenKind::COMMA {
            lexer.next_token()
            skip_nl()
            continue
        }

        if lexer.current_token_kind == TokenKind::NL {
            // At least one NL
            lexer.next_token()
            skip_nl()
            continue
        }

        // No delimiter, end of the loop
        break
    }

    // enumFields
    while lexer.current_token_kind == TokenKind::LET {
        skip_token_kind(TokenKind::LET)?
        let field_name = read_name()?
        skip_token_kind(TokenKind::COLON)?
        let type_usage = read_type_usage()?

        def.fields[] = NStructField @[
            name: field_name,
            type_usage,
        ]

        skip_nl()
    }

    skip_token_kind(TokenKind::RBRACE)?

    // TODO: Check for duplicate names
    let all_field_names: List<String> = []

    for field in def.fields {
        all_field_names[] = field.name
    }

    for entry in enum_entries {
        // Autogenerated constants
        let const_code = NCode::new()
        let const_def = NConst @[
            span: entry.span,
            path,
            name: entry.name,
            type_usage: NTypeUsage::simple_with_path(path, name),
            body: const_code,
        ]
        program.consts[] = const_def

        let type_usage = NTypeUsage::simple_with_path(path, name)
        let alloc = const_code.add_alloc(entry.span, type_usage).id

        let used_field_names: List<String> = []

        // Variant field
        let variant_value = const_code.add_lit_int(entry.span, entry.variant)
        const_code.add_store_field(entry.span, alloc, VARIANT_FIELD_NAME, variant_value.id)
        used_field_names[] = VARIANT_FIELD_NAME

        // Rest of fields
        for i in entry.value_init {
            const_code.merge(i.code)
            used_field_names[] = i.field_name

            if const_code.last_expression.is_none() {
                program.reporter.report_error("Expecting expression, not found", entry.span)
                continue
            }

            const_code.add_store_field(entry.span, alloc, i.field_name, const_code.last_expression!!)
        }

        // Missing fields to init?
        if all_field_names.len() > used_field_names.len() {
            let missing_fields: List<String> = []
            for field in all_field_names {
                if !used_field_names.contains(field) {
                    missing_fields[] = field
                }
            }

            let missing_fields_str = missing_fields.join(", ")
            program.reporter.report_error("Missing fields: $missing_fields_str", entry.span)
        }

        const_code.add_return(entry.span, alloc)

        // TODO: Autogenerated functions
        // fun values(): List<Direction> = #[Up, Down, Left, Right, Front, Back]
        // fun Direction.to_string(): String {
        //     if (this.variant == Direction::Up.variant) {
        //         return "Up"
        //     }
        //     if (this.variant == Direction::Down.variant) {
        //         return "Down"
        //     }
        //     // ... Rest of cases
        //     unreachable()
        // }
        // fun from_variant(index: Int): Optional<Direction> {
        //    if (index == 1) {
        //       return Optional::Some(Up)
        //    }
        //    ...
        //    return Optional::None
        //  }
        // fun Direction.get_ordering(other: Direction): Ordering {
        //     ret this.variant.get_ordering(other.variant)
        // }
        // fun Direction.is_equal(other: Direction): Boolean {
        //     ret this.variant.is_equal(other.variant)
        // }
    }

    ret found(nothing)
}