
//    annotation
//      : AT anyName annotationArgs? NL* ;
//
//    annotationArgs
//        : LBRACKET NL* (annotationArgEntry (commaOrNl annotationArgEntry)* COMMA?)? NL* RBRACKET ;
//
//    annotationArgEntry
//        : annotationArgKey COLON NL* constExpr ;
//
//    annotationArgKey
//        : anyName
//        | PLAIN_STRING
//        ;

// Parse a single annotation
fun Parser.read_annotation(): Parsed<NAnnotation> {
    let span = lexer.current_token_span
    skip_token_kind(TokenKind::AT)?
    let name: String = read_name()?

    let ann = NAnnotation @[span, name, args: []]

    if lexer.current_token_kind != TokenKind::LBRACKET {
        ret found(ann)
    }

    skip_token_kind(TokenKind::LBRACKET)?
    skip_nl()

    read_until_delimited(TokenKind::RBRACKET) @{
        ann.args[] = read_annotation_arg()?
        ret found(nothing)
    }?

    ret found(ann)
}

// Read a list of annotations
fun Parser.read_annotations(): Parsed<NAnnotationContainer> {
    let annotations = NAnnotationContainer::new()

    while lexer.current_token_kind == TokenKind::AT {
        annotations[] = read_annotation()?
        skip_nl()
    }

    ret found(annotations)
}

// Read a key-value pair in an annotation
fun Parser.read_annotation_arg(): Parsed<NAnnotationArg> {
    let span = lexer.current_token_span
    let key: String = read_const_string()?
    skip_nl()
    skip_token_kind(TokenKind::COLON)?
    let value: NValue = read_nvalue()?

    ret found(NAnnotationArg @[span, key, value])
}

// Read an string or an identifier
fun Parser.read_const_string(): Parsed<String> {
    ret when lexer.current_token_kind {
        TokenKind::PLAIN_STRING -> read_plain_string()
        TokenKind::IDENTIFIER -> read_name()
        else -> parse_error(ParseError::ExpectedConstName, lexer.current_token)
    }
}
