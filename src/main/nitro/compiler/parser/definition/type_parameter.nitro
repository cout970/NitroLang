
// typeParamsDef
//      : LTH NL* typeParamDef (commaOrNl typeParamDef)* COMMA? NL* GTH ;
//
// typeParamDef
//       : typeParameter COLON typeUsage (COMMA typeUsage)*
//       | typeParameter
//       ;
//
// // #T, #A, #B, List<#A>
// typeParameter
//       : HASH anyName ;

struct NGeneric {
    id: Id
    span: Span
    name: String
    source: NGenericSource
    bounds: List<NGenericBounds>
}

enum NGenericSource {
    Struct,
    Option,
    Function,
    Lambda,
    TypeAlias,
    Invalid,
}

struct NGenericParams {
    definitions: List<NGeneric>
    type_tokens: List<TypeToken>
    source: NGenericSource
}

struct NGenericBounds {
    name: String
    tag_definition: Optional<NTag>
}

fun NGenericParams::new(): NGenericParams {
    ret NGenericParams @[
        definitions: [],
        type_tokens: [],
        source: NGenericSource::Invalid
    ]
}

fun NGenericParams.get(name: String): Optional<NGeneric> {
    for def in definitions {
        if def.name == name {
            ret Some(def)
        }
    }
    ret None()
}

// <#T, #A>
fun Parser.read_type_params_def(): Parsed<Nothing> {
    if lexer.current_token_kind != TokenKind::LTH {
        ret found(nothing)
    }

    // Skip `<`
    lexer.next_token()

    read_until_delimited(TokenKind::GTH) @{
        current_generics.definitions[] = read_type_param_def()?
        ret found(nothing)
    }?

    ret found(nothing)
}

fun Parser.read_type_param_def(): Parsed<NGeneric> {
    // `#A: Type1 | Type2` or just `#B`
    let span = lexer.current_token_span
    let name = read_type_parameter()?

    let def = NGeneric @[
        id: id_provider.next(),
        span,
        name,
        source: current_generics.source,
        bounds: []
    ]

    if lexer.current_token_kind != TokenKind::COLON {
        ret found(def)
    }

    lexer.next_token()

    def.bounds[] = NGenericBounds @[name: read_name()?, tag_definition: None()]

    loop {
        // End of the list
        if lexer.current_token_kind != TokenKind::OR {
            break
        }

        // Break so read_type_params_def() can read the next item
        if lexer.next_token_kind == TokenKind::HASH {
            break
        }

        // Skip comma
        lexer.next_token()

        // Found new constraint
        if lexer.current_token_kind != TokenKind::HASH {
            def.bounds[] = NGenericBounds @[name: read_name()?, tag_definition: None()]
        }
    }

    ret found(def)
}

fun Parser.read_type_parameter(): Parsed<String> {
    if lexer.current_token_kind != TokenKind::HASH {
        ret parse_error(ParseError::ExpectedTypeParameter, lexer.current_token)
    }

    // Skip `#`
    lexer.next_token()

    ret read_name()
}

fun NGeneric.get_hash(): Int = id.get_hash()
fun NGeneric.get_ordering(other: NGeneric): Ordering = id.get_ordering(other.id)

fun NGeneric.to_string(): String {
    if bounds.is_empty() {
        ret "#$name"
    }
    let s = bounds.join_to_string(", ") @{ i -> i.name }

    ret "#$name: s"
}

fun NGenericParams.to_string(): String {
    if definitions.is_empty() {
        ret ""
    }

    let s = ""

    for def in definitions {
        if s.is_not_empty() {
            s += ", "
        }
        s += def.to_string()
    }

    ret "<$s>"
}

fun NGenericBounds.get_ordering(other: NGenericBounds): Ordering = name.get_ordering(other.name)

fun NGenericBounds.get_hash(): Int = name.get_hash()
