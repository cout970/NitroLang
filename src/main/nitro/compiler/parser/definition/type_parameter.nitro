
// typeParamsDef
//      : LTH NL* typeParamDef (commaOrNl typeParamDef)* COMMA? NL* GTH ;
//
// typeParamDef
//       : typeParameter COLON typeUsage (COMMA typeUsage)*
//       | typeParameter
//       ;
//
// // #T, #A, #B, List<#A>
// typeParameter
//       : HASH anyName ;

// <#T, #A>
fun Parser.read_type_params_def(): Parsed<Nothing> {
    if lexer.current_token_kind != TokenKind::LTH {
        ret found(nothing)
    }

    // Skip `<`
    lexer.next_token()

    read_until_delimited(TokenKind::GTH) @{
        read_type_param_def(current_generics)?
        ret found(nothing)
    }?

    ret found(nothing)
}

fun Parser.read_type_param_def(generics: NGenericParams): Parsed<Nothing> {
    // `#A: Type1 | Type2` or just `#B`
    // Also `#A: tag Type1 | tag Type2` is valid
    let span = lexer.current_token_span
    let name = read_type_parameter()?

    let def = NGeneric @[
        id: id_provider.next(),
        index: generics.definitions.len,
        span,
        name,
        source: generics.source,
        bounds: []
    ]

    generics[] = def

    if lexer.current_token_kind != TokenKind::COLON {
        ret found(nothing)
    }

    // Skip `:`
    lexer.next_token()

    // Read list of tag bounds delimited by `|`
    loop {
        // Skip tag
        if lexer.current_token_kind == TokenKind::TAG {
            lexer.next_token()
        }

        let requited_tag = read_name()?
        def.bounds[] = NGenericBounds @[name: requited_tag]

        // End of the list
        if lexer.current_token_kind != TokenKind::OR && lexer.current_token_kind != TokenKind::AND && lexer.current_token_kind != TokenKind::ADD {
            break
        }

        // Skip `|`
        lexer.next_token()
    }

    ret found(nothing)
}

fun Parser.read_type_parameter(): Parsed<String> {
    if lexer.current_token_kind != TokenKind::HASH {
        ret parse_error(ParseError::ExpectedTypeParameter, lexer.current_token)
    }

    // Skip `#`
    lexer.next_token()

    ret read_name()
}
