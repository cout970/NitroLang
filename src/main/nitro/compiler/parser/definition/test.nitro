
// E.g. test! "Check that everything works" { }
// testDefinition
//     : TEST NL* PLAIN_STRING NL* statementBlock ;

fun Parser.read_test_definition(annotations: List<NAnnotation>): Parsed<Nothing> {
    skip_token_kind(TokenKind::TEST)?
    skip_nl()

    let span = lexer.current_token_span
    let test_name = lexer.current_token_text
    skip_token_kind(TokenKind::PLAIN_STRING)?

    skip_nl()

    annotations[] = NAnnotation @[
        span,
        name: ANNOTATION_TEST,
        args: [
            NAnnotationArg @[
                span,
                key: "name",
                value: NConstExpr::NConstString @[value: test_name]
            ]
        ]
    ]

    let id = id_provider.next()
    let name = "test_${id}"

    let def = NFunction @[
        id,
        span,
        annotations,
        generics: NGenericParams::new(),
        has_receiver: false,
        path: "",
        name,
        parameters: [],
        return_type_usage: NTypeUsage::simple(span, "Nothing"),
        return_type: None(),
        parent_tag: None(),
        code: NCode::new(span, name),
        full_name_cache: None(),
    ]
    program.functions[] = def
    declare_name(get_full_path(def.path, def.name), def.span)

    read_code_block(def.code)?

    ret found(nothing)
}