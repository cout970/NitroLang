
// typeUsage
//       : THIS_TYPE
//       | LTH NL* typeUsage NL* GTH
//       | baseTypeUsage
//       | baseTypeUsage DOT functionTypeUsage
//       | functionTypeUsage
//       | typeParameter
//       ;
//
// // Int, List<Int>
// baseTypeUsage
//       : modulePath? anyName typeParamArg? ;
//
// // (Int) -> Int
// functionTypeUsage
//       : LPAREN RPAREN ARROW typeUsage
//       | LPAREN functionTypeUsageParam (commaOrNl functionTypeUsageParam)* RPAREN ARROW typeUsage
//       ;
//
// // a, a: Int
// functionTypeUsageParam
//       : typeUsage
//       | anyName COLON typeUsage
//       ;
//
// typeParamArg
//       : LTH NL* typeUsage (commaOrNl typeUsage)* COMMA? NL* GTH ;

struct NTypeUsage {
    span: Span
    kind: TypeUsageKind
    name: String
    path: String
    params: List<NTypeUsage>
    type_token: Optional<TypeToken>
    generic_definition: Optional<NGeneric>
}

enum TypeUsageKind {
    TYPE_SIMPLE,
    TYPE_PARAM,
    TYPE_UNRESOLVED,
}

// FIXME: temporary fix for a bug with the compiler not generating this function for enums automatically
fun TypeUsageKind.is_equal(other: TypeUsageKind): Boolean {
    ret this.variant == other.variant
}

fun NTypeUsage::unresolved(span: Span): NTypeUsage {
    ret NTypeUsage @[
        span,
        kind: TypeUsageKind::TYPE_UNRESOLVED,
        name: "[unresolved]",
        path: "",
        params: [],
        type_token: None(),
        generic_definition: None(),
    ]
}

fun NTypeUsage::simple(span: Span, name: String): NTypeUsage {
    ret NTypeUsage @[
        span,
        kind: TypeUsageKind::TYPE_SIMPLE,
        name,
        path: "",
        params: [],
        type_token: None(),
        generic_definition: None(),
    ]
}

fun NTypeUsage::simple_with_path(span: Span, path: String, name: String): NTypeUsage {
    ret NTypeUsage @[
        span,
        kind: TypeUsageKind::TYPE_SIMPLE,
        name,
        path: path,
        params: [],
        type_token: None(),
        generic_definition: None(),
    ]
}

fun Parser.read_type_usage(): Parsed<NTypeUsage> {
    let span = lexer.current_token_span

    // This
    if lexer.current_token_kind == TokenKind::THIS_TYPE {
        let span = lexer.current_token_span
        lexer.next_token()

        if current_tag.is_none() {
            report_error("'This' type can only be used in a Tag definition", span)
            ret found(NTypeUsage::unresolved(span))
        }
        // Translate
        // tag Foo { fun This.test() }
        // to
        // tag Foo { fun <#Foo: Tag> #Foo.test() }

        let instance = NGeneric @[
            id_wrapper: id_provider.next(),
            span,
            name: "This",
            source: NGenericSource::Function,
            bounds: [
                NGenericBounds @[
                    name: current_tag!!.name,
                    tag_definition: current_tag,
                ]
            ]
        ]
        current_generics.definitions[] = instance

        ret found(NTypeUsage @[
            span,
            kind: TypeUsageKind::TYPE_PARAM,
            name: "This",
            path: "",
            params: [],
            type_token: None(),
            generic_definition: Some(instance),
        ])
    }

    // #A
    if lexer.current_token_kind == TokenKind::HASH {
        lexer.next_token()
        let name: String = read_name()?
        let def: Optional<NGeneric> = current_generics[name]

        if def.is_none() && allow_generics_collection {
            let instance = NGeneric @[
               id_wrapper: id_provider.next(),
               span,
               name,
               source: current_generics.source,
               bounds: []
            ]
            current_generics.definitions[] = instance
            def = Some(instance)
        }

        ret found(NTypeUsage @[
            span,
            kind: TypeUsageKind::TYPE_PARAM,
            name,
            path: "",
            params: [],
            type_token: None(),
            generic_definition: def,
        ])
    }

    // (Int, Boolean)->Float
    if lexer.current_token_kind == TokenKind::LPAREN {
        ret read_function_type_usage(span, None())
    }

    // <T>
    if lexer.current_token_kind == TokenKind::LTH {
        lexer.next_token()

        skip_nl()
        let type_usage = read_type_usage()?
        skip_nl()

        skip_token_kind(TokenKind::GTH)?

        ret found(type_usage)
    }

    // core::Int, List<Int>
    let path: String = read_module_path()?
    let name: String = read_name()?
    let params: List<NTypeUsage> = []

    if lexer.current_token_kind == TokenKind::LTH {
        params = read_type_param_args()?
    }

    let ty = NTypeUsage @[
        span,
        kind: TypeUsageKind::TYPE_SIMPLE,
        name,
        path,
        params,
        type_token: None(),
        generic_definition: None(),
    ]

    // If `.(` is found after the name, it is a function type like `Int.() -> Nothing`
    if lexer.current_token_kind != TokenKind::DOT || lexer.next_token_kind != TokenKind::LPAREN {
        ret found(ty)
    }

    // Skip dot
    lexer.next_token()

    // Int.() -> Nothing
    ret read_function_type_usage(span, Some(ty))
}

// <A, B, C>
fun Parser.read_type_param_args(): Parsed<List<NTypeUsage>> {
    let list: List<NTypeUsage> = []
    skip_token_kind(TokenKind::LTH)?

    skip_nl()
    read_until_delimited(TokenKind::GTH) @{
        list[] = read_type_usage()?
        ret found(nothing)
    }?

    ret found(list)
}

// (Int, Boolean)->Float
fun Parser.read_function_type_usage(span: Span, arg1: Optional<NTypeUsage>): Parsed<NTypeUsage> {
    // // (Int) -> Int
    // functionTypeUsage
    //       : LPAREN RPAREN ARROW typeUsage
    //       | LPAREN functionTypeUsageParam (commaOrNl functionTypeUsageParam)* RPAREN ARROW typeUsage
    //       ;
    let params = []

    if arg1.is_some() {
        params[] = arg1!!
    }

    skip_token_kind(TokenKind::LPAREN)?
    skip_nl()
    read_until_delimited(TokenKind::RPAREN) @{
        params[] = read_function_type_usage_param()?
        ret found(nothing)
    }?

    skip_token_kind(TokenKind::ARROW)?

    params[] = read_type_usage()?

    ret found(NTypeUsage @[
        span,
        kind: TypeUsageKind::TYPE_SIMPLE,
        name: "Function",
        path: "",
        params,
        type_token: None(),
        generic_definition: None(),
    ])
}

fun Parser.read_function_type_usage_param(): Parsed<NTypeUsage> {
    // // a, a: Int
    // functionTypeUsageParam
    //       : typeUsage
    //       | anyName COLON typeUsage
    //       ;

    // Skip name, is only for documentation
    if lexer.next_token_kind == TokenKind::COLON {
        let name: String = read_name()?
        skip_token_kind(TokenKind::COLON)?
    }

    ret read_type_usage()
}

fun NTypeUsage.to_string(): String {
    let s = ""

    if kind == TypeUsageKind::TYPE_PARAM {
        ret "#$name"
    }

    if kind == TypeUsageKind::TYPE_UNRESOLVED {
        ret this.name
    }

    if path != "" {
        s = path + "::"
    }

    s = s + name

    if params.len > 0 {
        s = s + "<"
        repeat params.len {
            s += params[it]!!.to_string()
            if it < limit - 1 {
                s += ", "
            }
        }
        s = s + ">"
    }

    ret s
}