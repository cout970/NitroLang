
// // E.g. type Optional<T> {}
// optionDefinition
//     : OPTION modulePath? anyName typeParamsDef? NL* LBRACE NL*
//     (optionDefinitionItem (commaOrNl optionDefinitionItem)* COMMA?)? NL*
//     RBRACE ;
//
// // E.g. Some { value: T },
// optionDefinitionItem
//     : anyName structBody?;

struct NOption {
    span: Span
    annotations: List<NAnnotation>
    path: String
    name: String
    type_params: List<NTypeParameterDefinition>
    items: List<NOptionItem>
}

struct NOptionItem {
    span: Span
    index: Int
    parent: NOption
    def: NStruct
}

fun Parser.read_option_definition(annotations: List<NAnnotation>): Parsed<Nothing> {
    skip_token_kind(TokenKind::OPTION)?
    let span = lexer.current_token_span
    let path = read_declaration_module_path()?
    let name = read_name()?

    let def = NOption @[
        span,
        annotations,
        path,
        name,
        type_params: [],
        items: [],
    ]
    program.options[] = def
    program.declare_name(get_full_path(def.path, def.name), def.span)

    def.type_params = read_type_params_def()?
    skip_nl()

    skip_token_kind(TokenKind::LBRACE)?
    skip_nl()

    read_until_delimited(TokenKind::RBRACE) @{
        def.items[] = read_option_item(def)?
        ret found(nothing)
    }?

    ret found(nothing)
}

fun Parser.read_option_item(parent: NOption): Parsed<NOptionItem> {
    let span = lexer.current_token_span
    let name = read_name()?

    let def = NStruct @[
        span,
        annotations: [],
        path: parent.path,
        name,
        type_params: [],
        fields: [],
    ]

    if lexer.current_token_kind == TokenKind::LBRACE {
        skip_token_kind(TokenKind::LBRACE)?
        skip_nl()

        read_until_delimited_by(TokenKind::NL, TokenKind::RBRACE) @{
            def.fields[] = read_struct_field()?
            ret found(nothing)
        }?
    }

    ret found(NOptionItem @[
        span,
        index: parent.items.len(),
        parent,
        def,
    ])
}

fun NOption.to_string(): String {
    let prefix = ""

    for a in annotations {
        prefix = "$prefix$a\n"
    }

    let items_str = if items.len() > 0 { "{\n" } else { "{" }

    for item in items {
        let line = item.to_string().indent(2)
        items_str += "$line\n"
    }
    items_str = items_str + "}"
    let full_name = get_full_path(path, name)

    ret "${prefix}option $full_name $items_str // $span"
}

fun NOptionItem.to_string(): String {
    let prefix = ""

    for a in def.annotations {
        prefix = "$prefix$a\n"
    }

    let fields_str = if def.fields.len() > 0 { "{\n" } else { "{" }

    for field in def.fields {
        fields_str += "  ${field.name}: ${field.type_usage}\n"
    }
    fields_str = fields_str + "}"

    ret "${prefix}${def.name} $fields_str // $span"
}