
// structDefinition
//      : STRUCT modulePath? anyName typeParamsDef? NL* structBody;
//
// E.g. { a: Int, b: Int }
// structBody
//      : LBRACE NL* (structField (commaOrNl structField)* COMMA?)? NL* RBRACE ;
//
// E.g. value: Int,
// structField
//      : INTERNAL? anyName COLON typeUsage;

struct NStruct {
    id_wrapper: IdWrapper
    span: Span
    annotations: List<NAnnotation>
    path: String
    name: String
    fields: List<NStructField>
    generics: NGenericParams
    parent: Optional<NOption>
    is_enum: Boolean
    full_name_cache: Optional<String>
}

struct NStructField {
    span: Span
    name: String
    parent_struct: NStruct
    offset: Int
    type_usage: NTypeUsage
    field_type: Optional<Type>
}

fun Parser.read_struct_definition(annotations: List<NAnnotation>): Parsed<Nothing> {
    skip_token_kind(TokenKind::STRUCT)?
    let span = lexer.current_token_span
    let path = read_declaration_module_path()?
    let name = read_name()?

    let def = NStruct @[
        id_wrapper: id_provider.next(),
        span,
        annotations,
        path,
        name,
        generics: NGenericParams::new(),
        fields: [],
        parent: None(),
        is_enum: false,
        full_name_cache: None(),
    ]
    program.structs[] = def
    declare_name(get_full_path(def.path, def.name), def.span)

    start_generics_collection(def.generics, NGenericSource::Struct)
    defer { stop_generics_collection() }

    read_type_params_def()?

    skip_nl()
    skip_token_kind(TokenKind::LBRACE)?
    skip_nl()
    read_until_delimited(TokenKind::RBRACE) @{
        def.fields[] = read_struct_field(def)?
        ret found(nothing)
    }?

    ret found(nothing)
}

fun Parser.read_struct_field(def: NStruct): Parsed<NStructField> {
    let span = lexer.current_token_span
    let name = read_name()?

    skip_token_kind(TokenKind::COLON)?

    let type_usage = read_type_usage()?

    ret found(NStructField @[
        span,
        name,
        parent_struct: def,
        offset: def.fields.len,
        type_usage,
        field_type: None(),
    ])
}

fun NStruct.get_full_name(): String {
    if full_name_cache.is_some() {
        ret full_name_cache!!
    }

    let full_name = get_full_path(path, name)
    full_name_cache = Some(full_name)
    ret full_name
}

fun NStruct.get_ordering(other: NStruct): Ordering = this.id_wrapper.id <=> other.id_wrapper.id

fun NStruct.to_string(): String {
    let prefix = ""

    for a in annotations {
        prefix = "$prefix$a\n"
    }

    let fields_str = if fields.len > 0 { "{\n" } else { "{" }

    for field in fields {
        fields_str += "  ${field.name}: ${field.type_usage}\n"
    }
    fields_str = fields_str + "}"
    let full_name = get_full_path(path, name)

    ret "${prefix}struct $full_name $fields_str  // $span"
}